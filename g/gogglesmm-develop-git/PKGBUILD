# Maintainer: Sander Jansen <sander@knology.net>

pkgname=gogglesmm-develop-git
pkgver=1.2.1.r0.g19902e7
pkgrel=2
pkgdesc="Goggles Music Manager"
arch=(i686 x86_64 arm armv6h armv7h aarch64)
url="http://gogglesmm.github.io"
license=(GPL3)
depends=(openjpeg libepoxy libwebp taglib glu opus libmad faad2)
makedepends=(git cmake pkgconfig libpulse alsa-lib)
optdepends=('libpulse: PulseAudio Output'
            'alsa-lib: Alsa Output')
provides=(gogglesmm)
conflicts=(gogglesmm)
source=("git+https://github.com/gogglesmm/gogglesmm.git")
sha256sums=('SKIP')

pkgver() {
  cd "${pkgname/-develop-git/}"
  git describe --long | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd "${pkgname/-develop-git/}"
  cmake . \
        -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${pkgname/-develop-git/}"
  make DESTDIR="$pkgdir" install
}
