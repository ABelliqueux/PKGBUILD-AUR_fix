# Maintainer: Tony Lambiris <tony@criticalstack.com>

pkgname=gotop-git
pkgver=4.0.1.r11.ga8a238a
pkgrel=1
pkgdesc='A terminal based graphical activity monitor inspired by gtop and vtop'
arch=(x86_64 i686 arm armv6h armv7h aarch64)
url="https://github.com/xxxserxxx/gotop"
license=(AGPL)
makedepends=(go git)
conflicts=(gotop)
provides=(gotop)
source=("${pkgname%-git}::git+https://github.com/xxxserxxx/gotop")
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/${pkgname%-git}"
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd "$srcdir/${pkgname%-git}"

  export GOPATH="$SRCDEST/go-modules"

  go build \
    -gcflags "all=-trimpath=${PWD}" \
    -asmflags "all=-trimpath=${PWD}" \
    -ldflags "-extldflags ${LDFLAGS}" \
    -buildmode=pie \
    ./cmd/gotop
}

package() {
  cd "$srcdir/${pkgname%-git}"
  install -Dm755 gotop "${pkgdir}"/usr/bin/gotop
}
