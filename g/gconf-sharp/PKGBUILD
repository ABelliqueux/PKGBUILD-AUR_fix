# Maintainer:
# Contributor: tobias <tobias@archlinux.org>

pkgname=gconf-sharp
pkgver=2.24.4
pkgrel=6
arch=(x86_64)
license=(LGPL)
url="http://gtk-sharp.sourceforge.net"
pkgdesc="Mono bindings for GConf"
depends=(gtk-sharp-2 gconf)
source=("gnome-sharp-$pkgver.tar.gz::https://github.com/mono/gnome-sharp/archive/${pkgver}.tar.gz"
        "02_fix_pkg-config_paths.patch::https://aur.archlinux.org/cgit/aur.git/plain/02_fix_pkg-config_paths.patch?h=gnome-sharp"
        "04_initialize_dbus_glib_threading.patch::https://aur.archlinux.org/cgit/aur.git/plain/04_initialize_dbus_glib_threading.patch?h=gnome-sharp"
        "06_fix_mono_path.patch::https://aur.archlinux.org/cgit/aur.git/plain/06_fix_mono_path.patch?h=gnome-sharp")
sha256sums=('4e72b9b452af9cf54250564b0b633e5a5925f50ddb3b39e97efd0abe69bc077e'
            'f5b971c3101f91b6c2af89ce2af9a95cfa1435a08f95c341bfc2ca218a1e56dc'
            '508e171e4bd155ee0cb99374385460118d5f82998c21715e67a2f3267210170a'
            'ef439f99b61002253f8696611cb7e1d60168e85e058a555436b432ea709e21c8')

prepare() {
  cd "gnome-sharp-${pkgver}"
  patch -Np1 -i ../06_fix_mono_path.patch
  patch -Np1 -i ../04_initialize_dbus_glib_threading.patch
  patch -Np1 -i ../02_fix_pkg-config_paths.patch
}

build() {
  cd "gnome-sharp-${pkgver}"
  ./bootstrap-${pkgver//.4/} --prefix=/usr --sysconfdir=/etc
  make
}

package() {
  cd "gnome-sharp-${pkgver}"
  make -C gconf/GConf install DESTDIR="${pkgdir}"
  make -C gconf/tools install DESTDIR="${pkgdir}"
}
