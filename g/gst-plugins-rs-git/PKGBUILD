# Maintainer: robertfoster

pkgname=gst-plugins-rs-git
pkgver=r2383.f4cb4b9d
pkgrel=1
pkgdesc="GStreamer plugins written in Rust"
arch=(x86_64)
url="https://gstreamer.freedesktop.org/"
license=(Apache LGPL2.1 MIT MPL2)
depends=(gstreamer gtk4 dav1d libsodium libwebp)
makedepends=(git rust meson cargo-c clang hotdoc)
provides=(gst-plugins-rs)
conflicts=(gst-plugins-rs)
options=(!lto)
source=("git+https://gitlab.freedesktop.org/gstreamer/gst-plugins-rs.git#branch=0.9")
sha256sums=(SKIP)

pkgver() {
  cd "${pkgname%-git}"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd "${pkgname%-git}"
  cargo add 'dav1d@0.8' --manifest-path "${srcdir}/${pkgname%-git}/video/dav1d/Cargo.toml"
  cargo fetch --target x86_64-unknown-linux-gnu --manifest-path "${srcdir}/${pkgname%-git}/Cargo.toml"
}

build() {
  cd "${pkgname%-git}"
  arch-meson build \
    -D csound=disabled \
    -D dav1d=enabled \
    -D sodium=system

  ninja -C build
}

package() {
  cd "${pkgname%-git}"
  DESTDIR="${pkgdir}" meson install -C build
  install -D LICENSE-* -t "${pkgdir}/usr/share/licenses/${pkgname}"
}