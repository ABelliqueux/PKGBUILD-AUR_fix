# Maintainer: robertfoster

pkgname=gst-plugins-rs-git
pkgver=0.8.2.r0.g7d093ffc
pkgrel=1
pkgdesc="GStreamer plugins written in Rust"
arch=(x86_64)
url="https://gstreamer.freedesktop.org/"
license=(Apache LGPL2.1 MIT MPL2)
depends=(gstreamer gtk4 dav1d libsodium libwebp)
makedepends=(git rust meson cargo-c clang)
provides=(gst-plugins-rs)
conflicts=(gst-plugins-rs)
source=("git+https://gitlab.freedesktop.org/gstreamer/gst-plugins-rs.git#branch=0.8")
sha256sums=(SKIP)

pkgver() {
  cd "${pkgname%-git}"
  #printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
  # no tags on main
  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd "${pkgname%-git}"
  arch-meson build -D csound=disabled -D sodium=system 
  ninja -C build
}

package() {
  cd "${pkgname%-git}"
  DESTDIR="${pkgdir}" meson install -C build
  install -D LICENSE-* -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

