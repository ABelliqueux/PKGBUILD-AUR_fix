# Maintainer: Stefan Sielaff <aur AT stefan-sielaff DOT de>

pkgname=logitechmediaserver-git
pkgver=8.2.0.r77.g9089cfba8
pkgrel=1
pkgdesc='Slimserver for Logitech Squeezebox players. This server is also called Logitech Media Server. (Git-Version, if you prefer stability consider using logitechmediaserver instead)'
arch=(i686 x86_64 arm armv6h armv7h aarch64)
url='https://github.com/Logitech/slimserver'
license=(GPL custom)
depends=(perl perl-io-socket-ssl)
makedepends=(git yasm rsync)
optdepends_x86_64=('lib32-gcc-libs: transcoding on 64-bit systems')
#provides=(logitechmediaserver)
#conflicts=(logitechmediaserver)
install=logitechmediaserver.install
source=("git+https://github.com/Logitech/slimserver.git"
        "git+https://github.com/Logitech/slimserver-vendor.git"
        'logitechmediaserver.service')
sha256sums=('SKIP'
            'SKIP'
            '7b9cfcb0ae66d27afcaea0ab2f48f44b26a5ecbef1234aeccc4dbc63a1e6e36f')

pkgver() {
    cd "${srcdir}/slimserver"
    git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
    cd "${srcdir}/slimserver/Bin"
    rm -rf MSWin32-x86-multi-thread darwin darwin-x86_64 i386-freebsd-64int i86pc-solaris-thread-multi-64int powerpc-linux sparc-linux
    case $CARCH in
	x86_64) rm -rf {arm,armhf,aarch64}-linux ;;
	i686) rm -rf {arm,armhf,aarch64}-linux ;;
	aarch64) rm -rf {i386,x86_64,arm,armhf}-linux ;;
	arm*) rm -rf {i386,x86_64,aarch64}-linux ;;
    esac
    cd "${srcdir}/slimserver/CPAN"
    rm -rf arch
    cd "${srcdir}/slimserver/lib"
    rm -rf Audio
}

build() {
    cd "${srcdir}/slimserver-vendor/CPAN"
    sh buildme.sh -t
}

package() {
    cd "${srcdir}"
    install -Dm644 logitechmediaserver-git.service -t "${pkgdir}/usr/lib/systemd/system/"
    cd "${srcdir}/slimserver"
    install -d "${pkgdir}"/{opt,usr/share/licenses}/"${pkgname}" && ln -s "/opt/${pkgname}/License.txt" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    cp -a * "${pkgdir}/opt/${pkgname}"
    cd "${srcdir}/slimserver-vendor"
    cp -a CPAN/build/5.*/lib/*/*linux*/* "${pkgdir}/opt/${pkgname}/CPAN"
    printf "ARCH-AUR-GIT.%s\n%s" "${pkgver}" "$(date)" > "${pkgdir}/opt/${pkgname}/revision.txt"
}
