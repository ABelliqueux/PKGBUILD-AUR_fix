# Maintainer: 

pkgname=libtransmission
pkgver=3.00
pkgrel=1
arch=(i686 x86_64 arm armv6h armv7h aarch64)
url="http://www.transmissionbt.com/"
pkgdesc="Fast, easy, and free BitTorrent library"
license=(MIT)
depends=(openssl libnatpmp libevent libb64 miniupnpc curl)
makedepends=(git cmake libevent dht libutp)
source=("git+https://github.com/transmission/transmission.git#tag=${pkgver}")
sha256sums=('SKIP')

prepare() {
  cd "${srcdir}/transmission"

  [[ -d build ]] || mkdir build
}

build() {
  cd "${srcdir}/transmission/build"
  
  cmake .. \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=None \
    -DINSTALL_LIB=ON \
    -DENABLE_UTP=ON \
    -DENABLE_QT=OFF \
    -DENABLE_GTK=OFF \
    -DENABLE_CLI=OFF \
    -DENABLE_DAEMON=OFF \
    -DUSE_SYSTEM_UTP=ON \
    -DUSE_SYSTEM_DHT=ON \
    -DWITH_INOTIFY=OFF \
    -DWITH_KQUEUE=OFF \
    -DWITH_LIBAPPINDICATOR=OFF
  make

}

package() {
  cd "${srcdir}/transmission/build"
  make -C libtransmission DESTDIR="$pkgdir" install
  install -Dm644 ${srcdir}/transmission/COPYING "$pkgdir/usr/share/licenses/libtransmission/COPYING"
}
