# Maintainer: 

pkgname=libtransmission
pkgver=3.00
pkgrel=1
arch=(i686 x86_64 arm armv6h armv7h aarch64)
url="http://www.transmissionbt.com/"
pkgdesc="Fast, easy, and free BitTorrent library"
license=(MIT)
depends=(openssl libnatpmp curl libevent miniupnpc dht libutp libb64)
makedepends=(cmake libevent)
source=("git+https://github.com/transmission/transmission.git#tag=${pkgver}"
        'git+https://github.com/transmission/dht.git'
        'git+https://github.com/transmission/libb64.git'
        'git+https://github.com/transmission/libevent.git'
        'git+https://github.com/transmission/libnatpmp.git'
        'git+https://github.com/transmission/libutp.git'
        'git+https://github.com/transmission/miniupnpc.git')
sha256sums=('SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP')

prepare() {
  cd "${srcdir}/transmission"

  git submodule init
  git config submodule.third-party/dht.url "${srcdir}/dht"
  git config submodule.third-party/libb64.url "${srcdir}/libb64"
  git config submodule.third-party/libevent.url "${srcdir}/libevent"
  git config submodule.third-party/libnatpmp.url "${srcdir}/libnatpmp"
  git config submodule.third-party/libutp.url "${srcdir}/libutp"
  git config submodule.third-party/miniupnpc.url "${srcdir}/miniupnpc"
  git submodule update

  [[ -d build ]] || mkdir build
}

build() {
  cd "${srcdir}/transmission/build"
  
  cmake .. \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DINSTALL_LIB=ON \
    -DENABLE_UTP=ON \
    -DENABLE_QT=OFF \
    -DENABLE_GTK=OFF \
    -DENABLE_CLI=OFF \
    -DENABLE_DAEMON=OFF \
    -DCMAKE_BUILD_TYPE=Release \
    -DUSE_SYSTEM_UTP=ON

  make
}

package() {
  cd "${srcdir}/transmission/build"
  make -C libtransmission DESTDIR="$pkgdir" install
  install -Dm644 ${srcdir}/transmission/COPYING "$pkgdir/usr/share/licenses/libtransmission/COPYING"

}
