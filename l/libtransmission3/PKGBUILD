# Maintainer: 

pkgname=libtransmission3
pkgver=3.00
pkgrel=1
arch=(x86_64)
url="http://www.transmissionbt.com/"
pkgdesc="Transmission static library v3.00 and headers"
license=(MIT)
makedepends=(git cmake dht libutp libnatpmp libevent libb64 miniupnpc)
conflicts=(libtransmission)
source=("git+https://github.com/transmission/transmission.git#tag=${pkgver}")
sha256sums=('SKIP')

# using transmission-${pkgver}.tar.xz as source fails with
# No SOURCES given to target: transmission-test-subprocess
#source=("https://github.com/transmission/transmission/releases/download/${pkgver}/transmission-${pkgver}.tar.xz")
#cmake -B build -S "transmission-$pkgver" -Wno-dev \

build() {
  cmake -B build -S "transmission" -Wno-dev \
    -DCMAKE_BUILD_TYPE=RelWithDebInfo \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DINSTALL_LIB=ON \
    -DENABLE_UTP=ON \
    -DENABLE_QT=OFF \
    -DENABLE_GTK=OFF \
    -DENABLE_CLI=OFF \
    -DENABLE_UTILS=OFF \
    -DENABLE_TESTS=OFF \
    -DENABLE_DAEMON=OFF \
    -DUSE_SYSTEM_UTP=ON \
    -DUSE_SYSTEM_DHT=ON \
    -DWITH_INOTIFY=OFF \
    -DWITH_KQUEUE=OFF \
    -DWITH_LIBAPPINDICATOR=OFF

  cmake --build build
}

package() {
  DESTDIR="${pkgdir}" cmake --install build
  install -D -t "${pkgdir}/usr/share/licenses/${pkgname}/" transmission/COPYING

  #install -D build/libtransmission/libtransmission.a "${pkgdir}/usr/lib/libtransmission3.a"
}
