# Maintainer: Solomon Choina <shlomochoina@gmail.com>

pkgname=libutp-git
pkgver=r99.gfda9f4b
pkgrel=1
epoch=3
pkgdesc="uTorrent Transport Protocol library"
arch=(i686 x86_64)
url="https://github.com/transmission/libutp"
license=(MIT)
makedepends=(git cmake)
provides=(libutp)
conflicts=(libutp)
source=("git+https://github.com/transmission/libutp.git#branch=post-3.3-transmission")
sha256sums=('SKIP')

pkgver() {
  cd "${srcdir}/libutp"
  printf "r%s.g%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd "${srcdir}/libutp"
  [[ -d build ]] || mkdir build
}

build() {
  cd "${srcdir}/libutp/build"
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr
  make
}
package() {
  cd "${srcdir}/libutp/build"
  make DESTDIR=${pkgdir} install
  install -D ../LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
