# Maintainer: Solomon Choina <shlomochoina@gmail.com>

pkgname=libutp
pkgver=r164.gf22f814
pkgrel=1
pkgdesc="uTorrent Transport Protocol library"
arch=(i686 x86_64)
url="https://github.com/transmission/libutp"
license=(MIT)
makedepends=(git cmake)
source=("git+https://github.com/transmission/libutp.git#commit=f22f814b5d46ecabb0081a8a0b190f6e93ed1a4d")
sha256sums=('SKIP')

pkgver() {
  cd "${srcdir}/libutp"
  printf "r%s.g%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd "${srcdir}/libutp"
  [[ -d build ]] || mkdir build
}

build() {
  cd "${srcdir}/libutp/build"
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr
  make
}
package() {
  cd "${srcdir}/libutp/build"
  make DESTDIR=${pkgdir} install
  install -D ../LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
