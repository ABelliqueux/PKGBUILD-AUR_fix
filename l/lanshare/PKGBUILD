# Maintainer: sigmacool

pkgname=lanshare
pkgver=1.2.1
pkgrel=5
pkgdesc="LAN Share is a cross platform local area network file transfer application, built using Qt GUI framework."
arch=(i686 x86_64 arm armv6h armv7h aarch64)
url="https://github.com/abdularis/LAN-Share"
license=(GPL)
depends=(qt5-base)
source=("$pkgname-$pkgver.tar.gz::https://github.com/abdularis/LAN-Share/archive/$pkgver.tar.gz"
        "https://raw.githubusercontent.com/FabioLolix/AUR-artifacts/master/lanshare-desktop.patch")
sha256sums=('c2d8edbb71c524d52defb5f11790ded241908a47c329754cd95fdd1526e4c65e'
            'ea90b752004f74ffe2fa749883164f7d93581ce6e8d9073f6ad432ed4bee8627')

prepare() {
  cd "$srcdir/LAN-Share-$pkgver/packaging/linux/debian"
  patch LANShare.desktop -i "${srcdir}/lanshare-desktop.patch"
}

build() {
  cd "$srcdir/LAN-Share-$pkgver/src"
  qmake
  make
}

package() {
  install -Dm755 "$srcdir/LAN-Share-$pkgver"/src/LANShare "$pkgdir"/usr/bin/LANShare
  ln -s /usr/bin/LANShare "$pkgdir/usr/bin/lanshare"

  cd "$srcdir/LAN-Share-$pkgver/packaging/linux/debian"
  install -D LANShare.desktop  "$pkgdir"/usr/share/applications/LANShare.desktop
  install -D lanshare-icon.png "$pkgdir"/usr/share/pixmaps/lanshare-icon.png
}

