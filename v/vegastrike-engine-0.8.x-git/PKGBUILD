# Maintainer: Fabio 'Lolix' Loli <fabio.loli@disroot.org> -> https://github.com/FabioLolix
# Contributor: Evert Vorster <evorster@gmail.com>

pkgname=vegastrike-engine-git
pkgver=0.8.1.beta2.r23.g80ae1dc57
pkgrel=1
pkgdesc="A spaceflight simulator in massive universe"
arch=(x86_64)
url="https://www.vega-strike.org/"
license=(GPL2)
depends=(boost-libs python freeglut gtk3 libvorbis openal sdl glu)
makedepends=(git cmake boost ogre ffmpeg)
provides=(vegastrike-engine)
conflicts=(vegastrike-engine)
options=(!lto)
source=("git+https://github.com/vegastrike/Vega-Strike-Engine-Source#branch=0.8.x")
sha256sums=('SKIP')

pkgver() {
  cd Vega-Strike-Engine-Source
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare(){
  cd "${srcdir}/Vega-Strike-Engine-Source/engine"
  mkdir -p build
}

build(){
  cd "${srcdir}/Vega-Strike-Engine-Source/engine/build"
#buildtype none will fail the build, python error
  cmake .. -Wno-dev \
	-DCMAKE_BUILD_TYPE=RelWithDebInfo \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DDISABLE_FFMPEG=OFF \
    -DDISABLE_OGRE=OFF \
    -DENABLE_PIE=ON
  make
}

package() {
  cd "${srcdir}/Vega-Strike-Engine-Source/engine/"
  make -C build DESTDIR="${pkgdir}" install
  rm -rf ${pkgdir}/usr/include
  rm -rf ${pkgdir}/usr/lib
}
