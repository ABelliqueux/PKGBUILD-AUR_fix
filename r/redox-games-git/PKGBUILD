# Maintainer: Fabio 'Lolix' Loli <fabio.loli@disroot.org> -> https://github.com/FabioLolix
# Contributor: √Årni Dagur <arnidg@protonmail.ch>

pkgname=redox-games-git
pkgver=r149.94ba01f
pkgrel=1
pkgdesc="Games for Redox OS (and Linux) in the terminal"
arch=(x86_64 i686 pentium4 arm armv6h armv7h aarch64)
url="https://gitlab.redox-os.org/redox-os/games"
license=(MIT)
makedepends=(rust git)
source=("${pkgname%-git}::git+https://gitlab.redox-os.org/redox-os/games.git")
sha512sums=('SKIP')

pkgver() {
  cd "${pkgname%-git}"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "${pkgname%-git}"
  cargo build --release --locked
}

check() {
  cd "${pkgname%-git}"
  cargo test --release --locked
}

package() {
  cd "${pkgname%-git}"

  mkdir -p "$pkgdir/usr/bin"
  install -Dm755 target/release/{baduk,commie,dem,flappy,h4xx3r,ice,minesweeper,reblox,redoku,rusthello,snake} "$pkgdir/usr/bin"

  mkdir -p "$pkgdir/usr/share/license"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
