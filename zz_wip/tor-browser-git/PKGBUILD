# Maintainer: Your Name <youremail@domain.com>

pkgname=tor-browser-git
pkgver=r821791.190c4b37ac3d
pkgrel=1
pkgdesc="Web browser based on Firefox and Tor"
arch=(x86_64 aarch64)
url="https://www.torproject.org/"
license=(MPL GPL LGPL)
depends=(gtk3 libxt mime-types dbus-glib ffmpeg nss ttf-font libpulse)
makedepends=(unzip zip diffutils yasm mesa imake inetutils xorg-server-xvfb
             autoconf2.13 rust clang llvm jack nodejs cbindgen nasm
             python-setuptools python-zstandard lld dump_syms)
             #wasi-compiler-rt wasi-libc wasi-libc++ wasi-libc++abi
optdepends=('networkmanager: Location detection via available WiFi networks'
            'libnotify: Notification integration'
            'pulseaudio: Audio support'
            'speech-dispatcher: Text-to-Speech'
            'hunspell-en_US: Spell checking, American English'
            'xdg-desktop-portal: Screensharing with Wayland')
provides=(tor-browser)
conflicts=(tor-browser)
options=(!emptydirs !makeflags !strip !lto !debug)
source=("git+https://gitlab.torproject.org/tpo/applications/tor-browser.git")
sha256sums=('SKIP')
validpgpkeys=('E53D989A9E2D47BF' # "Tor Browser Developers (signing key) <torbrowser@torproject.org>"
              'EF6E286DDA85EA2A4BA7DE684E2C6E8793298290' # "Tor Browser Developers (signing key) <torbrowser@torproject.org>"
              )

pkgver() {
  cd tor-browser
  #git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p mozbuild
  cd tor-browser

  cat >../mozconfig <<END
ac_add_options --prefix=/usr
ac_add_options --enable-release
ac_add_options --enable-hardening
ac_add_options --enable-optimize
ac_add_options --enable-rust-simd
ac_add_options --enable-linker=lld

ac_add_options --disable-updater
ac_add_options --disable-tests
END
}

build() {
  cd tor-browser

  export MOZBUILD_STATE_PATH="$srcdir/mozbuild"
  export MOZ_ENABLE_FULL_SYMBOLS=1
  export MACH_USE_SYSTEM_PYTHON=1

  ./mach build
}

package() {
  cd tor-browser
  DESTDIR="$pkgdir" ./mach install
}
