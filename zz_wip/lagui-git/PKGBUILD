# Maintainer:

pkgname=lagui-git
pkgver=r94.0abf6a3
pkgrel=1
pkgdesc="OpenGL based data driven graphical application framework"
arch=(x86_64)
url="https://www.wellobserve.com/index.php?post=20221022165137"
license=(GPL3)
makedepends=(git cmake libgl freetype2 glew libxi libxcursor)
provides=(lagui)
conflicts=(lagui)
options=(!lto)
source=("git+https://www.wellobserve.com/repositories/chengdulittlea/LaGUI.git") 
sha256sums=('SKIP')

pkgver() {
  cd LaGUI
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd LaGUI
  [[ -d build ]] || mkdir build
}

build() {
  cd LaGUI/build
  cmake .. -Wno-dev \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd LaGUI/build
  make DESTDIR="${pkgdir}" install

  #fonts get installed in ~.local/share/fonts/lagui/
  rm -rf "${pkgdir}/home" #makepkg
  rm -rf "${pkgdir}/build" #devtools
}
