# Maintainer: 

pkgname=eos-easy-installer-git
pkgver=0.15.0.r4.g038c5f99
pkgrel=1
pkgdesc="/e/OS “easy” installer"
arch=(x86_64)
url="https://community.e.foundation/c/community/easy-installer"
license=(GPL3)
_java=11
depends=(jdk${_java}-openjdk java-runtime-common java-environment-common)
makedepends=(git)
provides=(eos-easy-installer)
conflicts=(eos-easy-installer)
source=("git+https://gitlab.e.foundation/e/tools/easy-installer.git"
        easy-installer.sh)
sha256sums=('SKIP'
            'bf9063f526f1d80b2a12c7da6fb026992864e57e84f9ced3cbde2cb81db09ce7')

pkgver() {
  cd "${srcdir}/easy-installer/"
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd "${srcdir}/easy-installer/"
  export JAVA_HOME="/usr/lib/jvm/java-${_java}-openjdk"
  #gradle -Dorg.gradle.daemon=false
  ./gradlew -Dorg.gradle.daemon=false dist
}

package() {
  install -d "${pkgdir}"/usr/share/easy-installer
  cp -ar "${srcdir}"/easy-installer/build/image/easy-installer-linux-x64/* "${pkgdir}"/usr/share/easy-installer
  install -Dm755 easy-installer.sh "${pkgdir}"/usr/bin/easy-installer
}