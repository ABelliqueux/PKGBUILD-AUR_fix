# Maintainer:

pkgname=ourpaint-git
pkgver=r71.e4cd5e7
pkgrel=1
pkgdesc="Paint program that uses programmable brushes using a visual language"
arch=(x86_64)
url="https://www.wellobserve.com/index.php?post=20230102172212"
license=(GPL3 'CCPL:cc-by-nc-2.0')
depends=(noto-fonts noto-fonts-cjk libxi libxcursor glew freetype2 lcms2)
makedepends=(git cmake lagui libpng libx11 libxext)
provides=(ourpaint)
conflicts=(ourpaint)
source=("git+https://www.wellobserve.com/repositories/chengdulittlea/OurPaint.git")
sha256sums=('SKIP')

pkgver() {
  cd OurPaint
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd OurPaint
  [[ -d build ]] || mkdir build
}

build() {
  cd OurPaint/build
  cmake .. -Wno-dev \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd OurPaint/build
  #make DESTDIR="${pkgdir}" install #No rule to make target 'install'
  install -D OurPaint -t ${pkgdir}/usr/bin
  ln -s "/usr/bin/OurPaint" "${pkgdir}/usr/bin/ourpaint"

  install -D ../{COPYING*,README} -t "${pkgdir}/usr/share/licenses/${pkgname}"
}