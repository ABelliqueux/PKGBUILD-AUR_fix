# Maintainer: Your Name <youremail@domain.com>

pkgname=design-git
pkgver=44.alpha1.r24.ga273f37
pkgrel=1
pkgdesc="(alpha) 2D CAD For GNOME"
arch=(any)
url="https://github.com/dubstar-04/Design"
license=(GPL3)
depends=(gjs gtk4 libadwaita)
makedepends=(git meson blueprint-compiler gtk-update-icon-cache desktop-file-utils)
checkdepends=(appstream appstream-glib)
provides=(design)
conflicts=(design)
source=("git+https://github.com/dubstar-04/Design.git"
        "git+https://github.com/dubstar-04/Design-Core.git")
sha256sums=('SKIP'
            'SKIP')

pkgver() {
  cd Design
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd Design
  git submodule init
  git config submodule.src/Design-Core.url "${srcdir}/Design-Core"
  git -c protocol.file.allow=always submodule update
}

build() {
  cd Design
  #arch-meson . build
  meson setup . build --prefix=/usr
  ninja -C build
}

package() {
  cd Design
  DESTDIR="${pkgdir}" ninja -C build install
}