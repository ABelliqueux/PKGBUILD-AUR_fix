# Maintainer: Your Name <youremail@domain.com>

pkgname=tor-browser-alpha
_alpha=11.5a13
_firefox=91.10.0esr
_pkgver=11.5-1-build2
pkgver=${_pkgver//-/.}
pkgrel=1
pkgdesc="Web browser based on Firefox and Tor"
arch=(x86_64 aarch64)
url="https://www.torproject.org/"
license=(MPL GPL LGPL)
depends=(gtk3 libxt mime-types dbus-glib ffmpeg nss ttf-font libpulse)
makedepends=(unzip zip diffutils yasm mesa imake inetutils xorg-server-xvfb
             autoconf2.13 rust clang llvm jack nodejs cbindgen nasm
             python-setuptools python-zstandard lld dump_syms)
             #wasi-compiler-rt wasi-libc wasi-libc++ wasi-libc++abi
optdepends=('networkmanager: Location detection via available WiFi networks'
            'libnotify: Notification integration'
            'pulseaudio: Audio support'
            'speech-dispatcher: Text-to-Speech'
            'hunspell-en_US: Spell checking, American English'
            'xdg-desktop-portal: Screensharing with Wayland')
provides=(tor-browser)
conflicts=(tor-browser)
options=(!emptydirs !makeflags !strip !lto !debug)
source=("https://dist.torproject.org/torbrowser/${_alpha}/src-firefox-tor-browser-${_firefox}-${_pkgver}.tar.xz"{,.asc})
sha256sums=('5cf5fcd08e0c7f8f4192ec48266426a267595f11f40156e91457530f8ab500d7'
            'SKIP')
validpgpkeys=('E53D989A9E2D47BF' # "Tor Browser Developers (signing key) <torbrowser@torproject.org>"
              'EF6E286DDA85EA2A4BA7DE684E2C6E8793298290' # "Tor Browser Developers (signing key) <torbrowser@torproject.org>"
              ) 

prepare() {
  mkdir -p mozbuild
  cd "$srcdir/firefox-tor-browser-${_firefox}-${_pkgver}"

  cat >../mozconfig <<END
ac_add_options --prefix=/usr
ac_add_options --enable-release
ac_add_options --enable-hardening
ac_add_options --enable-optimize
ac_add_options --enable-rust-simd
ac_add_options --enable-linker=lld

ac_add_options --disable-updater
ac_add_options --disable-tests
END
}

build() {
  cd "$srcdir/firefox-tor-browser-${_firefox}-${_pkgver}"

  export MOZBUILD_STATE_PATH="$srcdir/mozbuild"
  export MOZ_ENABLE_FULL_SYMBOLS=1
  export MACH_USE_SYSTEM_PYTHON=1

  ./mach build
}

package() {
  cd "$srcdir/firefox-tor-browser-${_firefox}-${_pkgver}"
  DESTDIR="$pkgdir" ./mach install
}
