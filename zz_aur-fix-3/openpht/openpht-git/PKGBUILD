# Maintainer: Norman Uittenbogaart <normanu@gmail.com>

pkgname=openpht-git
pkgver=1.8.0b.r0.gcf23f65fd
pkgrel=1
pkgdesc='OpenPHT is a community driven fork of Plex Home Theater '
arch=(x86_64)
url='https://github.com/RasPlex/OpenPHT/'
license=(GPL2)
provides=(openpht)
conflicts=(openpht)
depends=('alsa-lib' 'avahi' 'boost-libs' 'curl' 'expat' 'freetype2' 'fribidi'
         'gcc-libs' 'glew' 'glibc' 'glu' 'libcdio' 'libgl' 'libjpeg' 'libmad'
         'libmicrohttpd' 'libmpeg2' 'libpng' 'libpulse' 'libsamplerate' 'llvm-libs'
         'libssh' 'libtiff' 'libusb-compat' 'libva' 'libvdpau' 'libx11'
         'libxext' 'libxrandr' 'lzo' 'pcre' 'sdl' 'sdl_image'
libass rtmpdump libplist libmodplug
         'sdl_mixer' 'sqlite' 'smbclient' 'taglib' 'tinyxml' 'yajl' 'zlib') #openpht-ffmpeg
makedepends=('git' 'boost' 'cmake' 'doxygen' fontconfig
             'libcec' 'yasm' 'swig')
optdepends=('libcec: Pulse-Eight USB-CEC adapter support')
source=("${pkgname%-git}::git+https://github.com/RasPlex/OpenPHT.git#branch=openpht-1.9"
        "https://github.com/brfransen/OpenPHT/commit/77b1dd490365dc12b94415fa14438a476fd7cfea.patch"
        "https://github.com/brfransen/OpenPHT/commit/2e2be7551fad30e896f577e5677a8599b7013475.patch"
        "https://github.com/brfransen/OpenPHT/commit/72ee4f5210e8f95faa72e230f8895f5e9c06352d.patch"
        "https://github.com/brfransen/OpenPHT/commit/77d1ce3e643d595fc4ef23f2eb9f7ec72e167a0f.patch"
        'openpht.sh'
	    #'fribidi.patch'
	    'openpht.desktop')
sha256sums=('SKIP'
            'a1381f7caa29744de77a65bc43e777769c0b056d5e1e1c0326a8eba1e6aaf703'
            '2fe8eeb33afbd9bbd79aa5bfb67c8c719cfd40106a9de28c1c9942f452ae6018'
            'f5df601bc33203af9b1f5b80ebece395755b5da4477063b799a68f61e7e79c04'
            '29caa1f92d61b67c8197ff2bf79a9f01ed8d1c4d4a0dac5ea7a783237d3480ce'
            '4a205bea1318ec77f618887ef66f6b0490128476a976044e237031b15bc0c30a'
            'd54df978d20e05da23917c92aa0033aadbbc3c894a9f67941fd5f1b40f49da17')

pkgver() {
  cd "${pkgname%-git}"
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd "${pkgname%-git}"
  #patch -Np1 <../fribidi.patch
  [[ -d build ]] || mkdir build

  patch -Np1 -i ../77b1dd490365dc12b94415fa14438a476fd7cfea.patch
  patch -Np1 -i ../2e2be7551fad30e896f577e5677a8599b7013475.patch
  patch -Np1 -i ../72ee4f5210e8f95faa72e230f8895f5e9c06352d.patch
  patch -Np1 -i ../77d1ce3e643d595fc4ef23f2eb9f7ec72e167a0f.patch
}

build() {
  cd "${pkgname%-git}/build"

  cmake .. \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=RELEASE \
    -DUSE_INTERNAL_FFMPEG=ON \
    -DENABLE_AUTOUPDATE=OFF \
    -DENABLE_CEC=OFF \
    -DENABLE_PYTHON=OFF \
    -DENABLE_SHAIRPLAY=OFF \
    -DENABLE_SHAIRPORT=OFF
  make
}

package() {
  cd "${pkgname%-git}/build"

  make DESTDIR="${pkgdir}" install
  install -dm 755 "${pkgdir}"/usr/{lib/openpht,share/{applications,pixmaps}}
  rm -rf "${pkgdir}"/usr/bin/*.so
  #install -m 755 "${srcdir}"/openpht.sh "${pkgdir}"/usr/bin/openpht
  install -m 644 "${srcdir}"/openpht.desktop "${pkgdir}"/usr/share/applications/
  install -m 644 ../plex/Resources/plex-icon-256.png "${pkgdir}"/usr/share/pixmaps/openpht.png
}
