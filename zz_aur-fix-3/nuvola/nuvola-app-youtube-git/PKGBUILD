# Maintainer: Gabriel JÃ¼lke <pyriand3r at gmail dot com>

pkgname=nuvola-app-youtube-git
pkgver=2.3.r0.gdfaa8ed
pkgrel=1
pkgdesc="Integration of YouTube into your linux desktop via Nuvola Player."
arch=(any)
url="https://github.com/tiliado/nuvola-app-youtube"
license=(BSD)
depends=(nuvolaplayer)
makedepends=(git python-nuvolasdk scour)
provides=(nuvola-app-youtube)
conflicts=(nuvola-app-youtube)
source=("git+https://github.com/tiliado/nuvola-app-youtube.git")
md5sums=('SKIP')

pkgver() {
  cd "${srcdir}/nuvola-app-youtube"
  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd "${srcdir}/nuvola-app-youtube"
  ./configure --prefix=/usr 
  make all
}

package() {
  cd "${srcdir}/nuvola-app-youtube"
  make DESTDIR="${pkgdir}" install
  install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
