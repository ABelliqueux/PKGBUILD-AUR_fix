# Maintainer:
# Contributor: Fabio 'Lolix' Loli <fabio.loli@disroot.org> -> https://github.com/FabioLolix
# Contributor: Mark Wagie <mark dot wagie at tutanota dot com>
# Contributor: Laramy Black <laramy2020@gmail.com>

pkgname=nuvolaruntime-git
pkgver=4.24.0.dev.r4.g2e95e61
pkgrel=1
pkgdesc="Nuvola Apps Runtime - Tight integration of web apps with your desktop"
arch=(x86_64)
url="https://nuvola.tiliado.eu"
license=(BSD)
depends=(diorite python-gobject json-glib webkit2gtk
         libnotify gst-plugins-ugly libdri2 libpulse
         libdrm libsecret)
#libappindicator-gtk3 is mandatory if enabled, need ayatana-appindicator3-0.1
makedepends=(git vala)
#makedepends=(git vala standard nodejs-engine.io-client nodejs-unit.js)
provides=(nuvolaplayer)
conflicts=(nuvolaplayer)
source=("git+https://github.com/tiliado/nuvolaruntime.git")
sha256sums=('SKIP')

pkgver() {
	cd "${srcdir}/${pkgname%-git}"
	git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
	cd "${srcdir}/${pkgname%-git}"
	./waf configure \
        --prefix=/usr \
        --libdir=/usr/lib \
        --dummy-engine \
        --no-appindicator \
        --no-vala-lint \
        --no-js-lint \
        --no-strict \
        --no-unity

	./waf build -v
}

#check() {
#	cd "${srcdir}/${pkgname%-git}/build"
#	LD_LIBRARY_PATH+=:$PWD LD_PRELOAD=libnuvolaruntime-tests.so ./run-nuvolaruntime-tests 
#}

package() {
	cd "${srcdir}/${pkgname%-git}"
	./waf install --destdir="${pkgdir}"
	install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
