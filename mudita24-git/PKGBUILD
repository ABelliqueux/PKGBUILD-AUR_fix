# Contributor: Fabio 'Lolix' Loli <lolix@disroot.org>
# Contributor: Ricky Sheaves <ricky[at]westdowneast[dot]com>

pkgname=mudita24-git
pkgver=r16.e38b1a3
pkgrel=1
pkgdesc="Improved Control Panel for Ice1712 Audio Cards"
arch=(i686 x86_64)
url="https://github.com/NielsMayer/mudita24"
license=(GPL2)
depends=(alsa-lib gtk2)
makedepends=(git cmake)
source=("git+https://github.com/NielsMayer/mudita24"
	      mudita24.desktop
	      mudita24.png)
md5sums=( 'SKIP'
          '71d692e6edea031aa791c92862e5d95d'
          '3f039ae5900c449d1887e34ce1ebfc62')

pkgver() {
  cd "${pkgname%-git}"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "${pkgname%-git}/${pkgname%-git}"
  cmake . -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd "${pkgname%-git}/${pkgname%-git}"
  make DESTDIR="${pkgdir}" install

  install -D -m644 "${srcdir}/mudita24.desktop" "${pkgdir}/usr/share/applications/mudita24.desktop"
  install -D -m644 "${srcdir}/mudita24.png"     "${pkgdir}/usr/share/pixmaps/mudita24.png"
  
  install -D -m644 COPYING "$pkgdir/usr/share/licenses/mudita24/COPYING"
}
