# Maintainer: Jikstra <jikstra@disroot.org>

pkgname=deltachat-desktop
pkgver=1.14.1
pkgrel=1
pkgdesc="Privacy oriented chat application built on e-mail"
arch=(any)
url="https://delta.chat/en/"
license=(GPL3)
depends=(electron6)
makedepends=(npm nodejs git)
source=("git+https://github.com/deltachat/deltachat-desktop.git#tag=v${pkgver}"
        "git+https://github.com/cracker0dks/basicwebrtc.git"
        "deltachat-desktop.desktop"
        "deltachat-desktop.sh"
        )
sha256sums=('SKIP'
            'SKIP'
            'bb12448df6e2385b37fbfb24bfecec55018986d0dd412aa351bea6ef336089da'
            '6ad355414b02a0c07ffb391ecc8cca71ff72d9848e80f27d5b43c0ec9c2c8b3e')

prepare() {
    cd "${srcdir}/${pkgname}"
    git submodule init
    git config 'submodule.basicwebrtc.url' "${srcdir}/basicwebrtc"
    git submodule update
}

build() {
    cd "${srcdir}/${pkgname}"

    npm install --verbose
    npm run build

    # Delete development dependencies, we don't need them anymore
    npm prune --production 
}


package() {
    cd "${srcdir}/${pkgname}"
    
    install -d "${pkgdir}/usr/lib/deltachat"

    cp -r node_modules  images tsc-dist build html-dist _locales themes "${pkgdir}/usr/lib/deltachat"

    rm -rf "${pkgdir}/usr/lib/deltachat/node_modules/deltachat-node/prebuilds/win32-x64"
    rm -rf "${pkgdir}/usr/lib/deltachat/node_modules/deltachat-node/prebuilds/darwin-x64"
    rm -rf "${pkgdir}/usr/lib/deltachat/build/vcredist"
    find "${pkgdir}/usr/lib/deltachat/node_modules/" -name *.js.map -exec rm {} \;

    cp index.js package.json "${pkgdir}/usr/lib/deltachat"
    
    install -Dm644 "${srcdir}/deltachat-desktop.desktop" "${pkgdir}/usr/share/applications/deltachat.desktop"
    install -Dm755 "${srcdir}/deltachat-desktop.sh" "${pkgdir}/usr/bin/deltachat"
    
    install -Dm644 ./images/deltachat.png "${pkgdir}/usr/share/pixmaps/deltachat.png"
}
 
