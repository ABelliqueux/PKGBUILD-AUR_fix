# Maintainer: Solomon Choina <shlomochoina@gmail.com>

pkgname=dht-git
pkgver=r242.g80024e5
pkgrel=1
pkgdesc="BitTorrent DHT library"
arch=(i686 x86_64)
url="https://github.com/transmission/dht.git"
license=(MIT)
makedepends=(git cmake)
provides=(dht)
conflicts=(dht)
source=("git+https://github.com/transmission/dht.git#branch=post-0.24-transmission")
sha256sums=('SKIP')

pkgver() {
  cd "${srcdir}/dht"
  printf "r%s.g%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd "${srcdir}/dht"
  [[ -d build ]] || mkdir build
}

build() {
  cd "${srcdir}/dht/build"
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr
  make
}
package() {
  cd "${srcdir}/dht/build"
  make DESTDIR=${pkgdir} install
  install -D ../LICENCE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
