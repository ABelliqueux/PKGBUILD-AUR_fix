# Maintainer: KeyLo99 <keylo99official@gmail.com>

pkg=k3rmit
pkgname=k3rmit-git
pkgdesc="A VTE-based terminal emulator that aims to be simple, fast and effective."
pkgver=1.5.r0.g469446b
pkgrel=1
arch=(x86_64 i686 arm armv6h armv7h aarch64)
url="https://github.com/KeyLo99/k3rmit"
license=(GPL3)
depends=(gtk3 vte3)
makedepends=(cmake git)
source=("git+https://github.com/KeyLo99/k3rmit")
sha256sums=('SKIP')

pkgver() {
  cd "${pkgname%-git}"
  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd "${pkgname%-git}"
  install -d build
}

build() {
  cd "${pkgname%-git}/build"
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr
}

package() {
  cd "${pkgname%-git}/build"
  make DESTDIR="${pkgdir}" install
}
