# Maintainer: Fabio 'Lolix' Loli <fabio.loli@disroot.org> -> https://github.com/FabioLolix

pkgname=static-web-server-git
pkgver=2.16.0.r5.g6876a75
pkgrel=1
pkgdesc="Cross-platform, high-performance and asynchronous web server for static files-serving"
arch=(x86_64)
url="https://static-web-server.net"
license=(Apache MIT)
depends=(glibc gcc-libs)
makedepends=(git cargo)
provides=(static-web-server)
conflicts=(static-web-server)
options=(!lto)
source=("git+https://github.com/static-web-server/static-web-server.git")
sha256sums=('SKIP')

pkgver() {
  cd static-web-server
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd static-web-server
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd static-web-server
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  cargo build --frozen --release --all-features
}

package() {
  cd static-web-server
  install -D target/release/static-web-server -t ${pkgdir}/usr/bin
  ln -s /usr/bin/static-web-server "${pkgdir}/usr/bin/sws"

  install -D LICENSE* -t "${pkgdir}/usr/share/licenses/${pkgname}"

  install -D systemd/static-web-server.service -t ${pkgdir}/usr/lib/systemd/system/
  install -D systemd/static-web-server.socket -t ${pkgdir}/usr/lib/systemd/system/

  #install systemd/etc_default_static-web-server ${pkgdir}/etc/static-web-server # need info about this
}
