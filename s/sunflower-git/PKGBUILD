# Maintainer:

pkgname=sunflower-git
pkgver=0.4.62.r49.g841252d0
pkgrel=1
pkgdesc="Small and highly customizable twin-panel file manager for Linux with support for plugins"
arch=(any)
license=(GPL3)
url="https://sunflower-fm.org"
depends=(gtk3 python-gobject python-chardet librsvg)
makedepends=(git)
optdepends=('vte3: integrated vte-based terminal'
            'python-mutagen: audio-metadata support'
            'gvfs: mount-management')
provide=(sunflower)
conflicts=(sunflower)
source=("${pkgname%-git}::git+https://github.com/MeanEYE/Sunflower.git")
sha256sums=('SKIP')

pkgver() {
	cd "${srcdir}/${pkgname%-git}"
	git describe --long --tags | sed -r 's/([^-]*-g)/r\1/;s/-/./g'
}

build() {
	cd "${srcdir}/${pkgname%-git}"
    python setup.py build
}

package() {
	cd "${srcdir}/${pkgname%-git}"
    python setup.py install --root="$pkgdir/" --optimize=1  --skip-build
    install -Dm644 images/sunflower.png "${pkgdir}/usr/share/pixmaps/sunflower.png"
    rm "${pkgdir}/usr/share/sunflower/images/sunflower.png"
    rm -rd "${pkgdir}/usr/share/sunflower/trasnaltions"
    cp -r "${srcdir}"/sunflower/translations/ "${pkgdir}/usr/share/locale"
    rm -f "${pkgdir}"/usr/share/locale/*/LC_MESSAGES/*.po
    rm -f "${pkgdir}"/usr/share/locale/*.pot
}
