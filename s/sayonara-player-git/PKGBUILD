# Maintainer: Fabio 'Lolix' Loli <fabio.loli@disroot.org> -> https://github.com/FabioLolix
# Contributor: Fixed Torres <aur_linuxero@outlook.com>

pkgname=sayonara-player-git
pkgver=1.8.0.beta1.r36.g0bf3f8f9
pkgrel=1
pkgdesc="Small, clear and fast audio player for Linux written in C++, supported by the Qt framework. It uses Gstreamer as audio backend."
arch=(x86_64 i686 aarch64)
url="https://sayonara-player.com/"
license=(GPL3)
depends=(qt5-base gst-plugins-base gst-plugins-good)
makedepends=(cmake qt5-tools qt5-svg git gst-plugins-bad)
optdepends=('gst-libav: additional codecs'
            'gst-plugins-bad: additional codecs'
            'gst-plugins-ugly: additional codecs')
provides=(sayonara-player)
conflicts=(sayonara-player)
source=("git+https://gitlab.com/luciocarreras/sayonara-player.git")
sha512sums=('SKIP')

pkgver() {
  cd "${srcdir}/sayonara-player"
  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd "${srcdir}/sayonara-player"
  [[ -d build ]] || mkdir build
}
 
build() {
  cd "${srcdir}/sayonara-player/build"
  cmake .. \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Debug \
    -DWITH_TESTS=1
  make
}

check() {
  cd "${srcdir}/sayonara-player/build"
  make test || true
}
 
package() {
  cd "${srcdir}/sayonara-player/build"
  make DESTDIR="$pkgdir/" install
}

