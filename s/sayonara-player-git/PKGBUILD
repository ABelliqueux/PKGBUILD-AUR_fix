# Maintainer: Fixed Torres <aur_linuxero@outlook.com>

pkgname=sayonara-player-git
pkgver=1.4.1.stable1.r1.gfe4a6d9a
pkgrel=2
pkgdesc="Small, clear and fast audio player for Linux written in C++, supported by the Qt framework. It uses Gstreamer as audio backend."
arch=(i686 x86_64)
url="http://sayonara-player.com"
license=(GPL3)
depends=(qt5-base taglib gst-plugins-base gst-plugins-good)
optdepends=(gst-plugins-ugly gst-plugins-bad)
makedepends=(cmake qt5-tools git)
provides=(sayonara-player)
conflicts=(sayonara-player)
source=("${pkgname%-git}::git+https://git.sayonara-player.com/sayonara.git")
sha512sums=('SKIP')

pkgver() {
    cd "$srcdir/${pkgname%-git}"
    git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
    cd "$srcdir/${pkgname%-git}"
    install -d build
}
 
build() {
    cd "$srcdir/${pkgname%-git}/build"
    cmake .. -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE="Release" -DWITH_TESTS=1
    make
}
 
package() {
    cd "$srcdir/${pkgname%-git}/build"
    make DESTDIR="$pkgdir/" install
}


