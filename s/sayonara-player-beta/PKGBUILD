# Maintainer: 
# Contributor: Fixed Torres <aur_linuxero@outlook.com>

pkgname=sayonara-player-beta
_pkgver=1.5.2-beta3
pkgver=${_pkgver//-/_}
pkgrel=1
pkgdesc="Small, clear and fast audio player for Linux written in C++, supported by the Qt framework. It uses Gstreamer as audio backend."
arch=(i686 x86_64)
url="http://sayonara-player.com"
license=(GPL3)
depends=(qt5-base taglib gst-plugins-base gst-plugins-good)
makedepends=(cmake qt5-tools git gst-plugins-bad)
optdepends=('gst-libav: additional codecs'
            'gst-plugins-bad: additional codecs'
            'gst-plugins-ugly: additional codecs')
source=("https://gitlab.com/luciocarreras/sayonara-player/-/archive/${_pkgver}/sayonara-player-${_pkgver}.tar.gz")
sha512sums=('a3ce12582dc95a86e69f18e45610f5559508068a96e94fae7b3fa9878d232d4a391b890c09934fd09d3ded4ee30e093cd58d8b4f7172ba0dd6bbcc54923285c1')

prepare() {
  cd "$srcdir/sayonara-player-${_pkgver}"
  install -d build
}

build() {
  cd "$srcdir/sayonara-player-${_pkgver}/build"
  cmake .. \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DUSE_SYSTEM_TAGLIB=1 \
    -DWITH_TESTS=1
  make
}

check() {
  cd "$srcdir/sayonara-player-${_pkgver}/build"
  make test
}
 
package() {
  cd "$srcdir/sayonara-player-${_pkgver}/build"
  make DESTDIR="$pkgdir/" install
}
