# Maintainer: Fabio 'Lolix' Loli <fabio.loli@disroot.org> -> https://github.com/FabioLolix

pkgname=cyan-git
pkgver=r1039.52d22e9
pkgrel=1
pkgdesc="Open source prepress color converter and image editor"
arch=(x86_64 i686 arm armv6h armv7h aarch64)
url="https://cyan.fxarena.net/"
license=(GPL2)
depends=(qt5-base imagemagick)
makedepends=(git cmake)
source=("git+https://github.com/rodlie/cyan.git")
sha256sums=('SKIP')

pkgver() {
  cd "${srcdir}/${pkgname%-git}"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
#  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare(){
  cd "${srcdir}/${pkgname%-git}"
  install -d build
}

build() {
  cd "${srcdir}/${pkgname%-git}/build"
  cmake .. \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr

  make
}

package() {
  cd "${srcdir}/${pkgname%-git}/build"
  make DESTDIR="${pkgdir}" install
}
