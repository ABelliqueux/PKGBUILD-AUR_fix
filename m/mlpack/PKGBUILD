# Maintainer: jerry73204 <jerry73204@gmail.com>
# Contributor: Govind Gopakumar < govind.93 at gmail dot com>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgname=mlpack
pkgver=3.2.0
pkgrel=1
pkgdesc="a scalable c++ machine learning library"
arch=(x86_64)
url="http://www.mlpack.org"
license=(BSD)
depends=(armadillo boost lapack libxml2 cython python-numpy python-pandas ensmallen
         )
makedepends=(cmake txt2man python-setuptools python-pytest-runner)
source=("http://www.mlpack.org/files/${pkgname}-${pkgver}.tar.gz")
sha256sums=('f40339766be92114d0e29cf484fc6570ca33fb04f67bdfcebf99504e0c1de792')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  cmake . \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DCMAKE_INSTALL_LIBDIR=lib \
      -DBUILD_PYTHON_BINDINGS=ON \
      -DFORCE_CXX11=ON \
      -DBUILD_MARKDOWN_BINDINGS=ON
  make
}

# check() {
#   # Comment out check() if there are failed tests. If you consider it a bug,
#   # please file an issue on upstream instead of AUR.

#   cd "${srcdir}/${pkgname}-${pkgver}"

#   make mlpack_test
#   bin/mlpack_test
# }

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="$pkgdir/" install

  install -m 755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m 644 LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}"

  rm -rf ${pkgdir}/usr/lib/python3.7/site-packages/{site.py,__pycache__,easy-install.pth}
  rm -rf ${pkgdir}/usr/bin/{cygdb,cython,cythonize}
  rm -rf ${pkgdir}/usr/bin/{f2py,f2py3.7}
}
