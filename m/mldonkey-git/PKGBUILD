# Maintainer: Anatol Pomozov <anatol.pomozov@gmail.com>

pkgname=mldonkey-git
pkgver=3.1.7.2.r9.g6de7bffa
pkgrel=1
pkgdesc='A multi-network P2P client'
arch=(x86_64)
url='http://mldonkey.sourceforge.net/'
license=(GPL)
depends=(file gd hicolor-icon-theme miniupnpc libnatpmp libminiupnpc.so)
makedepends=(git gtk2 librsvg ocaml-num camlp4 lablgtk2)
optdepends=('librsvg: GUI support'
            'gtk2: GUI support')
backup=(etc/conf.d/mldonkey)
source=("git+https://github.com/ygrek/mldonkey.git"
        "https://raw.githubusercontent.com/gentoo/gentoo/master/net-p2p/mldonkey/files/cpp17-byte-namespace.patch"
        "https://github.com/ygrek/mldonkey/pull/66/commits/20ff84c185396f3d759cf4ef46b9f0bd33a51060.patch"
        "https://github.com/ygrek/mldonkey/pull/66/commits/4c7206b2cb6e9be6cec587da9153e3d9932d05e8.patch"
        mldonkey.conf
        mldonkey.service
        mldonkey.tmpfiles
        mldonkey.sysusers)
sha256sums=('SKIP'
            'eef21187cecc6f1052ff6e0d988093a6fed8b4745ecb257b9e127c23c60cb27d'
            '2ad62fb539579cb67f86924eba3cf185eec99f36db072eb35c1a1ed1f6e25169'
            '2d6fa5e8d2806cb7397db8ef90e69dbf09f699f9d019567c8586cfaf83ef5520'
            'f1d9401cefd591662d49011c53fdb2788755a6f745a963e46d8037b990edeb6a'
            '778cebe8edcffd63db3594054c2daa62ce571644a96ad235b8c95470b55c0415'
            '9c78fbfbba4f8286e2c2299e4da6f76d0f34f33fde26964922707c34fb75157b'
            '24d7ef8f6af93a8d87a82842b0ed796e35ce2f88d81734d9275eede8f4e10fba')

pkgver() {
  cd "mldonkey"
  git describe --long --tags | sed 's/^release.//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd "mldonkey"
  #patch -Np1 -i ../cpp17-byte-namespace.patch
  patch -Np1 -i ../20ff84c185396f3d759cf4ef46b9f0bd33a51060.patch
  patch -Np1 -i ../4c7206b2cb6e9be6cec587da9153e3d9932d05e8.patch
}

build() {
  cd "mldonkey"
  ./configure \
    --prefix=/usr \
    --enable-gui=newgui2 \
    --enable-upnp-natpmp \
    --enable-batch

  make depend
  make
  make utils
}

package() {
  cd "mldonkey"
  make DESTDIR="$pkgdir" install

  install -Dm644 packages/rpm/mldonkey-icon-16.png "$pkgdir"/usr/share/icons/hicolor/16x16/apps/mldonkey.png
  install -Dm644 packages/rpm/mldonkey-icon-32.png "$pkgdir"/usr/share/icons/hicolor/32x32/apps/mldonkey.png
  install -Dm644 packages/rpm/mldonkey-icon-48.png "$pkgdir"/usr/share/icons/hicolor/48x48/apps/mldonkey.png
  install -Dm644 distrib/mldonkey.desktop "$pkgdir"/usr/share/applications/mldonkey.desktop

  install -Dm644 "$srcdir"/mldonkey.conf "$pkgdir"/etc/conf.d/mldonkey
  install -Dm644 "$srcdir"/mldonkey.service "$pkgdir"/usr/lib/systemd/system/mldonkey.service
  install -Dm644 "$srcdir"/mldonkey.tmpfiles "$pkgdir"/usr/lib/tmpfiles.d/mldonkey.conf
  install -Dm644 "$srcdir"/mldonkey.sysusers "$pkgdir"/usr/lib/sysusers.d/mldonkey.conf
}
