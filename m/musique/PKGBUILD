# Maintainer: mrxx <mrxx at cyberhome dot at>
# Contributor: Frederic Bezies < fredbezies at gmail dot com>
# Contributor: jddolvin <jddolvin@atheist.com>
# Contributor: arriagga <ramon.arriaga@gmail.com>
# Contributor: dieghen89 <dieghen89@gmail.com>

pkgname=musique
pkgver=1.9
pkgrel=1
pkgdesc="A finely crafted music player"
arch=(x86_64)
url="https://flavio.tordini.org/musique"
license=(GPL3)
depends=(qt5-base taglib mpv)
makedepends=(qt5-tools)
source=("https://github.com/flaviotordini/musique/releases/download/${pkgver}/musique-${pkgver}.tar.bz2")
sha256sums=('c914e768c84031658b5a9760eb44af9f21bc29097a1b5b8261b5053ea1b853a4')

prepare() {
  cd "${pkgname}-${pkgver}"
  install -d build
}

build() {
  cd "${pkgname}-${pkgver}/build"
  qmake-qt5 .. PREFIX=/usr
  make
}

package() {
  cd "${pkgname}-${pkgver}/build"
  make INSTALL_ROOT="$pkgdir" install
}

