# Maintainer: John A. Leuenhagen <john@zlima12.com>

pkgname=cpr
pkgver=1.10.5
pkgrel=1
pkgdesc='C++ Requests: Curl for People, a spiritual port of Python Requests.'
arch=(x86_64 i686)
url="https://github.com/libcpr/cpr"
license=(MIT)
depends=(glibc gcc-libs curl openssl)
makedepends=(cmake)
checkdepends=(gtest)
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/libcpr/cpr/archive/refs/tags/$pkgver.tar.gz")
sha256sums=('c8590568996cea918d7cf7ec6845d954b9b95ab2c4980b365f582a665dea08d8')

build() {
  cmake -B build -S "cpr-${pkgver}" \
    -Wno-dev \
    -DCMAKE_BUILD_TYPE=RelWithDebInfo \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCPR_BUILD_TESTS=ON \
    -DCPR_USE_SYSTEM_GTEST=ON \
    -DCPR_USE_SYSTEM_CURL=ON

  cmake --build build
}

check() {
  ctest --test-dir build --output-on-failure
}

package() {
  DESTDIR="${pkgdir}" cmake --install build
  install -D "cpr-${pkgver}/LICENSE" -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
