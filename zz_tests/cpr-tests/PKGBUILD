# Maintainer: John A. Leuenhagen <john@zlima12.com>

pkgname=cpr
pkgver=1.10.3
pkgrel=1
pkgdesc='C++ Requests: Curl for People, a spiritual port of Python Requests.'
arch=(x86_64 i686)
url="https://github.com/libcpr/cpr"
license=(MIT)
depends=(glibc gcc-libs curl openssl)
makedepends=(cmake)
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/libcpr/cpr/archive/refs/tags/$pkgver.tar.gz")
sha256sums=('7b9d3504ffdaf7ce3189f1b91d135888776b6a1b26ea1bf2c3c4986b8a5af06f')

build() {
  cmake -B build -S "cpr-${pkgver}" \
    -Wno-dev \
    -DCMAKE_BUILD_TYPE=RelWithDebInfo \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCPR_BUILD_TESTS=OFF \
    -DCPR_USE_SYSTEM_CURL=ON

  cmake --build build
}

package() {
  DESTDIR="${pkgdir}" cmake --install build
  install -D "cpr-${pkgver}/LICENSE" -t "${pkgdir}/usr/share/licenses/${pkgname}"
}