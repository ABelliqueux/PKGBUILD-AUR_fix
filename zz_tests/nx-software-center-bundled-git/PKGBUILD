# Maintainer: Fabio 'Lolix' Loli <fabio.loli@disroot.org> -> https://github.com/FabioLolix

pkgname=nx-software-center-git
pkgver=1.1.1.r6.gf9255c7
pkgrel=1
pkgdesc="Graphical front end to manage AppImage applications built using MauiKit"
arch=(x86_64)
url="https://github.com/Nitrux/nx-software-center"
license=(LGPL2.1)
depends=(mauikit mauikit-filebrowsing libappimage)
makedepends=(git cmake extra-cmake-modules argagg nlohmann-json xxd wget)
provides=(nx-software-center)
conflicts=(nx-software-center)
source=("git+https://github.com/Nitrux/nx-software-center.git"
        "libappimage-gcc13-fix.patch::https://github.com/AppImageCommunity/libappimage/pull/184/commits/76e298ecff5e5580ca634bef4bd472b8f418c417.patch"
        "zsync2-gcc13-fix.patch::https://github.com/AppImageCommunity/zsync2/pull/70/commits/4643977da83c041147c98d17a2edfb0d2764a3ea.patch")
sha256sums=('SKIP'
            '2209ca5fc8396e56efdc3b423fe32e08639e9a1eb849228c8531156edbed5f25'
            '16431809051aef97ad5b4cd6cab4f71b85712e9edfd13de245ad204faa830317')

pkgver() {
  cd nx-software-center
  git describe --long --tags --exclude latest | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd nx-software-center
  [[ -d build ]] || mkdir build
}

build() {
  cd nx-software-center/build
  cmake .. -Wno-dev \
    -DCMAKE_BUILD_TYPE=RelWithDebInfo \
    -DCMAKE_INSTALL_PREFIX=/usr

  make || true

  cd "${srcdir}/nx-software-center/build/AppImageUpdate-prefix/src/AppImageUpdate-build/_deps/libappimage-src"
  patch -Np1 -i "${srcdir}/libappimage-gcc13-fix.patch"

  cd "${srcdir}/nx-software-center/build/AppImageUpdate-prefix/src/AppImageUpdate-build/_deps/zsync2-src"
  patch -Np1 -i "${srcdir}/zsync2-gcc13-fix.patch"

  cd ${srcdir}/nx-software-center/build
  make
}

package() {
  cd nx-software-center/build
  make DESTDIR="${pkgdir}/" install
}
