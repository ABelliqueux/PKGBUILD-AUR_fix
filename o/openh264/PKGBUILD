# Maintainer: drakkan <nicola.murino at gmail dot com>

pkgname=openh264
pkgver=2.0.0
pkgrel=1
pkgdesc="OpenH264 is a codec library which supports H.264 encoding and decoding"
arch=(x86_64 i686 arm armv6h armv7h aarch64)
url="http://www.openh264.org/"
license=(BSD)
makedepends=(nasm)
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/cisco/openh264/archive/v${pkgver}.tar.gz")
sha256sums=('73c35f80cc487560d11ecabb6d31ad828bd2f59d412f9cd726cc26bfaf4561fd')

build() {
  cd "openh264-$pkgver"
  make
}

package() {
  cd "openh264-$pkgver"
  make DESTDIR="$pkgdir" PREFIX=/usr install
  install -Dm755 h264dec "$pkgdir"/usr/bin/h264dec
  install -Dm755 h264enc "$pkgdir"/usr/bin/h264enc
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

