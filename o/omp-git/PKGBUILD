# Maintainer: DJ Griffin <thewisenoob@gmail.com>

pkgname=omp-git
pkgver=0.1.1.r4.g20b68e5
pkgrel=1
pkgdesc='Open-Source Music Player'
url='https://openmusicplayer.com/'
arch=(x86_64)
license=(GPL3)
depends=(gtkmm3 libconfig libclastfm sqlite taglib
         gst-plugins-base gst-plugins-good)
optdepends=('gst-libav: additional codecs'
            'gst-plugins-bad: additional codecs'
            'gst-plugins-ugly: additional codecs')
makedepends=(git)
provides=(omp)
conflicts=(omp)
source=("${pkgname%-git}::git+https://github.com/TheWiseNoob/OpenMusicPlayer.git")
sha256sums=('SKIP')

pkgver() {
  cd "${pkgname%-git}"
  git describe --long --tags | sed -r 's/([^-]*-g)/r\1/;s/-/./g'
}

build() {
  cd "${pkgname%-git}"
  make -e FLATPAK=0 CXXFLAGS='-g -DFLATPAK=$(FLATPAK) -std=c++17 -Wall -pipe -Wno-deprecated-declarations -Wno-parentheses -fconcepts -Wno-catch-value -Wno-return-type'
}

package() {
  cd "${pkgname%-git}"
  make install DESTDIR=${pkgdir}
}
