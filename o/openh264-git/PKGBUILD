# Maintainer: Gustavo Alvarez <sl1pkn07@gmail.com>
# Contributor: sekret, mail=$(echo c2VrcmV0QHBvc3Rlby5zZQo= | base64 -d)

pkgname=openh264-git
pkgver=2.0.0.r141.g992c1c14
pkgrel=1
pkgdesc="OpenH264 is a codec library which supports H.264 encoding and decoding"
arch=(x86_64 i686 arm armv6h armv7h aarch64)
url="http://www.openh264.org"
license=(BSD)
makedepends=(git nasm)
provides=(openh264)
conflicts=(openh264)
source=("git+https://github.com/cisco/openh264.git")
sha256sums=('SKIP')

pkgver() {
  cd "${pkgname%-git}"
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd "${pkgname%-git}"
  make
}

package() {
  cd "${pkgname%-git}"
  make DESTDIR="${pkgdir}" PREFIX=/usr install
  install -Dm755 h264dec "$pkgdir/usr/bin/h264dec"
  install -Dm755 h264enc "$pkgdir/usr/bin/h264enc"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

