# Contributor: Bitts311 <oceanmail311@gmail.com>

pkgname=mplayer-gui-svn
pkgver=r38313
pkgrel=3
epoch=1
pkgdesc='Media player for Linux - SVN version'
url='http://www.mplayerhq.hu/'
arch=(i686 x86_64)
license=(GPL3)
depends=(gtk2 enca lirc libdca libbluray libpulse libxss faad2 jack2 libtheora dav1d libvorbis faac libvpx libglvnd
         xvidcore openjpeg2 libmad aom openal libbs2b a52dec aalib libdvdnav lame libmng opencore-amr libcdio-paranoia
         libcaca libvdpau libass libxv gsm mpg123 smbclient libx264.so libmpeg2)

makedepends=(
             ffmpeg sdl_image x264 yasm git subversion)

#libdca libxinerama libxxf86vm libxvmc libcdio rtmpdump libdvdread alsa-lib giflib fribidi freetype2 libgl speex
#ladspa rtmpdump libdvdcss fribidi opus libjpeg libpng libogg libx11 libxext

conflicts=(mplayer mencoder)
provides=(mplayer mencoder)
source=("mplayer::svn://svn.mplayerhq.hu/mplayer/trunk"
        "git+https://git.ffmpeg.org/ffmpeg.git"
	    "https://mplayerhq.hu/MPlayer/skins/Blue-1.13.tar.bz2"
	    'include-samba-4.0.patch'
        'revert-icl-fixes.patch')
sha512sums=('SKIP'
            'SKIP'
            '81205cc2334453ed23d94b9bb9efb9828764ba573abb745da0ae0fbffab019df996b2799a54a9a7b6eb77dac39925403131121d100588c4a7526d8b9dab474d8'
            '9debb8c58b996f6c716c22c86c720bf9dc49b4ee9b76c57519f791667ae1de2cc6f5add878fbf4ac02c6b6fd1865e1bcfa6105e75de4bf7ec596c338ed0cae99'
            '0cae0b26d3d97fd4c962962c43a481de20335369cbca406cadfc9bda1a0608b32f5374e76c477cb9a85bda83a568a1ed17126df224ae61579d0a402c1824aea8')

pkgver() {
  cd "mplayer"
  local ver="$(svnversion)"
  printf "r%s" "${ver//[[:alpha:]]}"
}

prepare() {
  cd "mplayer"

  patch -p1 < "../include-samba-4.0.patch"
  patch -p0 < "../revert-icl-fixes.patch"
  sed -E "s|(#include <samba-4.0/libsmbclient.h>)|#include <time.h>\n\1|g" -i stream/stream_smb.c
  ./version.sh
  cp -r "$srcdir/ffmpeg" "$srcdir/mplayer"
}


build() {
  cd "mplayer"
  ./configure \
    --prefix=/usr \
    --enable-runtime-cpudetection \
    --enable-gui  \
    --enable-gl \
    --language=all \
    --language-msg=all \
    --enable-smb

  make
}

package() {
  cd "mplayer"
  make DESTDIR="$pkgdir" install
  mv $srcdir/Blue $pkgdir/usr/share/mplayer/skins/default
}

