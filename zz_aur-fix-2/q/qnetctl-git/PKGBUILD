# Maintainer: Thomas LÃ¼bking <thomas.luebking@gmail.com>

pkgname=qnetctl-git
pkgver=r28.e3edb1c
pkgrel=1
epoch=1
pkgdesc="A Qt GUI for netctl"
arch=('i686' 'x86_64')
url="https://github.com/luebking/qnetctl/"
license=(GPL2)
depends=(qt5-base)
makedepends=(git)
conflicts=(qnetctl)
provides=(qnetctl)
source=("git+https://github.com/luebking/qnetctl.git")
sha256sums=('SKIP')

pkgver() {
    cd "${srcdir}/qnetctl"
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
    cd "${srcdir}/qnetctl"
    qmake-qt5 qmake.pro
    make
}

package() {
    cd "${srcdir}/qnetctl"
    install -D qnetctl "${pkgdir}/usr/bin/qnetctl"
    install -D qnetctl_tool "${pkgdir}/usr/bin/qnetctl_tool"
}
