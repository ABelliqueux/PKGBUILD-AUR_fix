# Maintainer: nezbednik <nezbedniklmao at gmail dot com>

pkgname=unrar-libre-git
pkgver=r10.g0ae0ae4
pkgrel=1
pkgdesc="Free software RAR archive extractor compatible with proprietary WinRAR CLI"
arch=(x86_64 i686 pentium4 arm armv6h armv7h aarch64)
url="https://github.com/abelianring/unrar-libre"
license=('custom: BlueOak-1.0.0')
makedepends=(git go)
provides=(unrar)
conflicts=(unrar)
source=("git+https://github.com/abelianring/unrar-libre.git")
sha256sums=('SKIP')

pkgver() {
  cd "${srcdir}/unrar-libre"
  printf "r%s.g%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}/unrar-libre/cmd/unrar"
  export GOFLAGS="-buildmode=pie -trimpath -ldflags=-linkmode=external"
  export CGO_LDFLAGS="${LDFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  go build -o unrar
}

package() {
  cd "${srcdir}/unrar-libre/cmd/unrar"
  install -Dm755 unrar -t "${pkgdir}/usr/bin/"
  install -Dm644 "${srcdir}/unrar-libre/LICENSE-BlueOak-1.0.0.md" -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
