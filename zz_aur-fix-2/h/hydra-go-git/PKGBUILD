# Maintainer: Your Name <youremail@domain.com>

pkgname=hydra-go-git
pkgver=2.2.0.r13.gbfb723d
pkgrel=1
pkgdesc="Command line utility for generating language-specific project structures."
arch=(x86_64 i686)
url="https://github.com/Shravan-1908/hydra.git"
license=(MIT)
makedepends=(git go)
source=("hydra-go::git+https://github.com/Shravan-1908/hydra.git")
sha256sums=('SKIP')

pkgver() {
  cd "${srcdir}/hydra-go"
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd "${srcdir}/hydra-go/src"

  go build \
    -gcflags "all=-trimpath=${PWD}" \
    -asmflags "all=-trimpath=${PWD}" \
    -ldflags "-extldflags ${LDFLAGS}" \
    -buildmode=pie
}

check() {
  cd "${srcdir}/hydra-go/src"
  go test -v
}

package() {
  cd "${srcdir}/hydra-go/src"
  install -Dm755 src "${pkgdir}/usr/bin/hydra-go"
  install -D ../LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
