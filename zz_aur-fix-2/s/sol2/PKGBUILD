# Maintainer: Carlos Aznar√°n <caznaranl@uni.pe>
# Contributor: TheAifam5 <theaifam5 at gmail com>

pkgname=sol2
pkgver=3.2.3
pkgrel=2
pkgdesc="C++ <-> Lua API wrapper with advanced features and top notch performance"
arch=(any)
url="https://sol2.rtfd.io"
license=(MIT)
makedepends=(cmake)
optdepends=(lua lua51 lua53 lua52 luajit)
options=(!strip)
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/ThePhD/sol2/archive/v${pkgver}.tar.gz")
sha512sums=('448be11555725ddce5ea8d56d793da529c1bb969a4f444ad840044c49a047a5af2abe96d7bf17763246a397eddb4a34e6d299adb14ad3f30f6193f631f27c239')

prepare() {
  cd "sol2-${pkgver}"
  [[ -d build ]] || mkdir build
}

build() {
  cd "sol2-${pkgver}/build"
  cmake .. -Wno-dev \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "sol2-${pkgver}/build"
  make DESTDIR="${pkgdir}" install
  install -D ../LICENSE.txt -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
