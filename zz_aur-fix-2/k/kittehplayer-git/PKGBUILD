# Maintainer: Kitteh <namedkitten@gmail.com>

pkgname=kittehplayer-git
pkgver=r430.9a0c08f
pkgrel=1
pkgdesc="A Qt Video Player with many themes for YouTube, Niconico and RoosterTeeth"
arch=(x86_64 i686 arm armv6h armv7h aarch64)
url="https://github.com/NamedKitten/KittehPlayer"
license=(MPL2)
depends=(qt5-svg qt5-declarative qt5-quickcontrols qt5-quickcontrols2 qt5-graphicaleffects mpv)
makedepends=(git cmake qt5-x11extras)
optdepends=('ffmpegthumbnailer: playlist thumbnails support'
            'youtube-dl: for sites like YouTube and Dailymotion')
source=("${pkgname%-git}::git+https://github.com/NamedKitten/KittehPlayer")
sha256sums=("SKIP")

pkgver() {
	cd "$srcdir/${pkgname%-git}"
	printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd "$srcdir/${pkgname%-git}"
  install -d build
}

build() {
  cd "$srcdir/${pkgname%-git}/build"
  cmake .. \
    -DCMAKE_UNITY_BUILD=yes \
    -DCMAKE_UNITY_BUILD_BATCH_SIZE=100 \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "$srcdir/${pkgname%-git}/build"
  make DESTDIR="$pkgdir/" install
  ln -s /usr/bin/KittehPlayer "$pkgdir/usr/bin/kittehplayer"
}
