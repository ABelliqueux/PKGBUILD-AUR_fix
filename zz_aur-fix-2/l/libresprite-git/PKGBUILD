# Maintainer: Kuba Kowalik <bell@0x07.pl>

pkgname=libresprite-git
pkgver=1.2.1.Beta.r15.g19a42fda
pkgrel=1
pkgdesc="Animated sprite editor & pixel art tool, fork of last GPL version of aseprite"
arch=(x86_64 i686 i486 pentium4 arm armv6h armv7h aarch64)
url="https://libresprite.github.io/"
license=(GPL)
depends=(libx11 pixman curl giflib zlib libpng libjpeg-turbo tinyxml freetype2 libwebp)
makedepends=(git cmake gtest)
provides=(libresprite)
conflicts=(libresprite)
source=("git+https://github.com/LibreSprite/LibreSprite.git"
        "aseprite-simpleini::git+https://github.com/aseprite/simpleini.git"
        "aseprite-duktape::git+https://github.com/aseprite/duktape.git"
        "aseprite-flic::git+https://github.com/aseprite/flic.git"
        "aseprite-clip::git+https://github.com/aseprite/clip.git"
        "git+https://github.com/dacap/observable.git"
        "aseprite-undo::git+https://github.com/aseprite/undo.git")
sha256sums=('SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP')

pkgver() {
  cd "${srcdir}/LibreSprite"
  printf "r%s.g%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
  #git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd "${srcdir}/LibreSprite"

  git submodule init
  git config submodule.third_party/simpleini.url "${srcdir}/aseprite-simpleini"
  git config submodule.third_party/duktape.url "${srcdir}/aseprite-duktape"
  git config submodule.src/flic.url "${srcdir}/aseprite-flic"
  git config submodule.src/clip.url "${srcdir}/aseprite-clip"
  git config submodule.src/observable.url "${srcdir}/observable"
  git config submodule.src/undo.url "${srcdir}/aseprite-undo"
  git submodule update

  [[ -d build ]] || mkdir build
}

build() {
  cd "${srcdir}/LibreSprite/build"

  cmake .. -Wno-dev \
    -DUSE_SHARED_PIXMAN=ON \
    -DWITH_WEBP_SUPPORT=ON \
    -DCMAKE_INSTALL_PREFIX=/usr

  make
}

package() {
  cd "${srcdir}/LibreSprite/build"

  make DESTDIR="${pkgdir}" install/fast
  install -Dm644 ../desktop/libresprite.desktop -t "$pkgdir/usr/share/applications"
  install -Dm644 ../data/icons/ase48.png -t "$pkgdir/usr/share/pixmaps"
}
