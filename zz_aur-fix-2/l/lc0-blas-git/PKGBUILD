# Maintainer: Valerii Huz <ghotrix at gmail dot com>

pkgname=lc0-blas-git
_pkgname=lc0-blas
pkgver=0.28.0.r0.g3982cc0e
pkgrel=1
pkgdesc="BLAS version of Leela Chess Zero."
arch=(x86_64)
url="https://lczero.org/"
license=(GPL3)
depends=(blas protobuf)
makedepends=(git meson ninja )
source=("${_pkgname}::git+https://github.com/LeelaChessZero/lc0.git#branch="release/0.28""
        "git+https://github.com/LeelaChessZero/lczero-common.git")
md5sums=('SKIP'
         'SKIP')

pkgver() {
    cd "${srcdir}/${_pkgname}"
    git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
    cd "${srcdir}/${_pkgname}"
    git submodule init
    git config submodule.libs/lczero-common.url "${srcdir}/lczero-common"
    git submodule update
}

build() {
    cd "${srcdir}/${_pkgname}"
    CC=gcc CXX=g++ ./build.sh release -Dopencl=false -Ddefault_library=static
}

package() {
    cd "$srcdir/$_pkgname/build/release"
    install -Dm755 lc0 $pkgdir/usr/bin/lc0
}
