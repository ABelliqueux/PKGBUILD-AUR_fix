# Maintainer: Daniel Milde <daniel@milde.cz>

pkgname=gdu-git
pkgver=4.11.2.r1.g0f6922d
pkgrel=1
pkgdesc="Fast disk usage analyzer"
arch=('x86_64')
url="https://github.com/dundee/gdu"
license=('MIT')
depends=(glibc)
makedepends=(go pandoc git)
providess=(gdu)
conflicts=(gdu)
source=("git+https://github.com/Dundee/gdu")
sha256sums=('SKIP')

pkgver() {
  cd "${pkgname%-git}"
  git describe --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare(){
  cd "${pkgname%-git}"
  mkdir -p dist/
}

build() {
  cd "${pkgname%-git}"
  local _BUILDINFO="-X 'github.com/dundee/gdu/v4/build.Version=${pkgver}' \
                    -X 'github.com/dundee/gdu/v4/build.User=${PACKAGER}' \
                    -X 'github.com/dundee/gdu/v4/build.Time=$(date)'"
  go build \
    -trimpath \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-linkmode external -extldflags \"${LDFLAGS}\" ${_BUILDINFO}" \
    -o dist/gdu \
    github.com/dundee/gdu/v4/cmd/gdu
  pandoc gdu.1.md -s -t man > dist/gdu.1
}

check() {
  cd "${pkgname%-git}"
  go test -v ./...
}

package() {
  cd "${pkgname%-git}"
  install -Dm755 dist/gdu       "${pkgdir}"/usr/bin/gdu
  install -Dm644 dist/gdu.1     "${pkgdir}"/usr/share/man/man1/gdu.1
  install -D -m644 LICENSE.md   "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

