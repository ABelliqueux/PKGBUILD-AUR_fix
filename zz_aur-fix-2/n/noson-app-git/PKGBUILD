# Maintainer: Matthias Baur <aur@matthiasbaur.me>

pkgname=noson-app-git
pkgver=4.4.9.r2.g2fedb7e3
pkgrel=1
pkgdesc="The essential to control music from your SONOS devices on Linux platforms"
arch=(x86_64 armv7h aarch64)
url="https://janbar.github.io/noson-app"
license=(GPL3)
depends=(qt5-base qt5-quickcontrols2 qt5-graphicaleffects qt5-svg libpulse flac)
makedepends=(cmake git)
source=("git+http://github.com/janbar/noson-app.git")
sha256sums=('SKIP')

pkgver() {
  cd noson-app
  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd noson-app
  [[ -d build ]] || mkdir build
}

build() {
  cd noson-app/build
  cmake .. -Wno-dev \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_INSTALL_PREFIX=/usr

  make
}

package() {
  cd noson-app/build
  make DESTDIR="${pkgdir}/" install
}
