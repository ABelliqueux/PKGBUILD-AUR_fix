# Maintainer: J.D. Broberg <jdbroberg72 at gmail dot com>

pkgname=odin-git
_pkgname=odin
pkgver=r4306.63282290
pkgrel=1
pkgdesc="compiler for the odin programming language"
arch=(x86_64)
url="http://github.com/odin-lang/odin"
license=(BSD)
depends=(glibc ncurses)
makedepends=(git clang clang+llvm-binaries)
provides=(odin)
conflicts=(odin)
source=("git+https://github.com/odin-lang/odin.git"
        '0001-patch-makefile-for-aur.patch')
sha256sums=('SKIP'
            '2fe8dc71165f99b3fbafa566ed90510106077a2be4ac137056321f5ec8f55d8c')

#export CC=/opt/clang+llvm11/bin/clang CXX=/opt/clang+llvm11/bin/clang++

pkgver() {
    cd "${_pkgname}"
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
    cd "${_pkgname}"
    patch --forward --strip=1 --input="${srcdir}/0001-patch-makefile-for-aur.patch"
    ln -s /opt/clang+llvm11 llvm
}

build() {
    cd "${_pkgname}"
    make release
}

package() {
    cd "${_pkgname}"

    install -d "${pkgdir}/opt/${_pkgname}"
    install -Dm755 "./odin" "${pkgdir}/opt/${_pkgname}/odin"

    install -d "${pkgdir}/usr/bin"
    ln -sf "/opt/${_pkgname}/odin" "${pkgdir}/usr/bin/odin"

    cp -av "./core" "${pkgdir}/opt/${_pkgname}"

    install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}
