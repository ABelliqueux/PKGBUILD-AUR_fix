# Maintainer: Fabio 'Lolix' Loli <lolix@disroot.org> -> https://github.com/FabioLolix
# Contributor: Peter <craven@gmx.net>

pkgname=pktriggercord-git
pkgver=0.84.04.r106.gb9132dc
pkgrel=1
pkgdesc="Remote control (tethering) for Pentax DSLR cameras"
arch=(i686 x86_64 armv6h armv7h aarch64)
url="http://pktriggercord.melda.info/"
license=(LGPL3)
depends=(gtk2)
makedepends=(git)
source=("git+https://github.com/asalamon74/pktriggercord.git#commit=b9132dc94b0f6cdc4442a537cc90d8389ac7a445")
sha256sums=('SKIP')

pkgver() {
  cd "${pkgname%-git}"
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd "${pkgname%-git}"
  sed -i 's|PREFIX ?=.*|PREFIX = /usr|' Makefile
}

build() {
  cd "${pkgname%-git}"
  make LDFLAGS=-lm PREFIX=/usr
}

package() {
  cd "${pkgname%-git}"
  make DESTDIR="$pkgdir/" install
}
