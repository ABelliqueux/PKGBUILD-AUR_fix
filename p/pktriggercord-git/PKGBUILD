# Maintainer: Fabio 'Lolix' Loli <lolix@disroot.org> -> https://github.com/FabioLolix
# Contributor: Peter <craven@gmx.net>

pkgname=pktriggercord-git
pkgver=0.85.00.r66.gc1177a4
pkgrel=1
pkgdesc="Remote control (tethering) for Pentax DSLR cameras"
arch=(i686 x86_64 armv6h armv7h aarch64)
url="https://pktriggercord.melda.info/"
license=(LGPL3)
depends=(gtk2)
makedepends=(git)
provides=(pktriggercord)
conflicts=(pktriggercord)
source=("git+https://github.com/asalamon74/pktriggercord.git")
sha256sums=('SKIP')

pkgver() {
  cd "${pkgname%-git}"
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

package() {
  cd "${pkgname%-git}"
  CFLAGS+=" -Isrc/external/js0n"
  make LDFLAGS=-lm PREFIX=/usr DESTDIR="${pkgdir}/" install
}
