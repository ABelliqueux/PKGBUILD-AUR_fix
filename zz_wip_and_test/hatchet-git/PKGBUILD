# Maintainer: 

pkgname=hatchet-git
pkgver=r11465.3d3bebfeb
pkgrel=1
pkgdesc="Multi-source music player"
arch=(x86_64 i686 armv6h armv7h aarch64)
url="https://github.com/hatchet-player/Hatchet"
license=(GPL3)
depends=(qt5-base qt5-svg qt5-tools qt5-webengine qt5-webkit qt5-x11extras
         vlc attica qtkeychain
         liblastfm-qt5
         boost sparsehash lucene++ qca quazip telepathy-qt miniupnpc)
makedepends=(git meson ninja cmake gcc-objc)
optdepends=()
provides=(hatchet)
conflicts=(hatchet)
options=()
source=("${pkgname%-git}::git+https://github.com/hatchet-player/Hatchet.git"
        "git+https://github.com/hatchet-player/libportfwd"
        "git+https://github.com/hatchet-player/kdsingleapplicationguard"
        "git+https://github.com/hatchet-player/hatchet-plugins"
        "git+https://github.com/hatchet-player/libcrashreporter-qt"
        "git+https://github.com/hatchet-player/qxtweb-standalone"
        "git+https://github.com/hatchet-player/qt-certificate-addon"
        "git+https://github.com/hatchet-player/sparsehash"
        "git+https://github.com/hatchet-player/SPMediaKeyTap")
sha256sums=('SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP')

pkgver() {
  cd "${pkgname%-git}"
  ( set -o pipefail
    git describe --long 2>/dev/null | sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
  )
}

prepare() {
  cd "${pkgname%-git}"
  [[ -d build ]] || mkdir build

  git submodule init
  git submodule set-url subprojects/libportfwd "${srcdir}/libportfwd"
  git submodule set-url subprojects/kdsingleapplicationguard "${srcdir}/kdsingleapplicationguard"
  git submodule set-url subprojects/hatchet-plugins "${srcdir}/hatchet-plugins"
  git submodule set-url subprojects/libcrashreporter-qt "${srcdir}/libcrashreporter-qt"
  git submodule set-url subprojects/qxtweb-standalone "${srcdir}/qxtweb-standalone"
  git submodule set-url subprojects/qt-certificate-addon "${srcdir}/qt-certificate-addon"
  git submodule set-url subprojects/sparsehash "${srcdir}/sparsehash"
  git submodule set-url subprojects/SPMediaKeyTap "${srcdir}/SPMediaKeyTap"
  git submodule update
}

build() {
  cd "${pkgname%-git}"
  meson setup build --prefix=/usr -D with_echonest=false 
# -D with_liblastfm=false -D with_telepathy=false
  ninja -C build
}

package() {
  cd "${pkgname%-git}"
  DESTDIR="${pkgdir}" ninja -C build install
}
