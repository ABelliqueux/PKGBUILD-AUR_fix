# Maintainer:
# Contributor:

pkgname=crowdsec-git
pkgver=1.0.14.r10.g6693bff2
pkgrel=1
pkgdesc="Open-source massively multiplayer firewall"
arch=(x86_64 i686 arm armv6h armv7h aarch64)
url="https://crowdsec.net/"
license=(MIT)
depends=(glibc)
makedepends=(git go jq)
provides=(crowdsec)
conflicts=(crowdsec)
source=("git+https://github.com/crowdsecurity/crowdsec.git")
sha256sums=('SKIP')

pkgver() {
  cd "${srcdir}/${pkgname%-git}"
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd "${srcdir}/${pkgname%-git}"
  export GOPATH="$SRCDEST/go-modules"
  make release
}

package() {
  cd "${srcdir}/${pkgname%-git}"
  install -d "${pkgdir}/usr/bin"
  install -m755 cmd/crowdsec/crowdsec "${pkgdir}/usr/bin/crowdsec"
  install -m755 cmd/crowdsec-cli//cscli "${pkgdir}/usr/bin/cscli"

  install -d "${pkgdir}/etc/crowdsec"
  cp -r config/* "${pkgdir}/etc/crowdsec"

  install -d "${pkgdir}/usr/lib/systemd/system"
  mv "${pkgdir}/etc/crowdsec/crowdsec.service" "${pkgdir}/usr/lib/systemd/system"

  install -D -m644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
