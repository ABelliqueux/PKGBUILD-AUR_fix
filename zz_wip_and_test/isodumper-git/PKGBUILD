# Maintainer: Fabio 'Lolix' Loli <lolix@disroot.org> -> https://github.com/FabioLolix

pkgname=isodumper-git
pkgver=1.33.r8.g46feeb4
pkgrel=1
pkgdesc="A tool for writing ISO images on a USB stick, a fork of usb-imagewriter"
arch=(any)
url="http://gitweb.mageia.org/software/isodumper"
license=(GPL LGPL2.1)
depends=(python udisks2)
makedepends=(git intltool imagemagick pango)
provides=(isodumper)
conflicts=(isodumper)
source=("git://git.mageia.org/software/isodumper")
md5sums=('SKIP')

pkgver() {
  cd "${pkgname%-git}"
  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd "${pkgname%-git}"
  make
}

package() {
  cd "${pkgname%-git}"
  make DESTDIR=${pkgdir} install
}
