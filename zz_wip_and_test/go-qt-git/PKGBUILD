# Maintainer: 

pkgname=go-qt-git
pkgver=r832.5074eb6d
pkgrel=1
pkgdesc="Qt binding for Go"
arch=(x86_64 aarch64)
url="https://github.com/therecipe/qt"
license=(LGPL3)
depends=(glibc)
makedepends=(go git)
source=("${pkgname%-git}::git+https://github.com/therecipe/qt.git")
sha256sums=('SKIP')

pkgver() {
  cd "${pkgname%-git}"
  ( set -o pipefail
    git describe --long 2>/dev/null | sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
  )
}

build() {
  export GOPATH="$SRCDEST/go-modules"

  cd "${pkgname%-git}/cmd/qtdeploy"
  go build -gcflags "all=-trimpath=${PWD}" -asmflags "all=-trimpath=${PWD}" -ldflags "-extldflags ${LDFLAGS}" -buildmode=pie

  cd "${srcdir}/${pkgname%-git}/cmd/qtminimal"
  go build -gcflags "all=-trimpath=${PWD}" -asmflags "all=-trimpath=${PWD}" -ldflags "-extldflags ${LDFLAGS}" -buildmode=pie

  cd "${srcdir}/${pkgname%-git}/cmd/qtmoc"
  go build -gcflags "all=-trimpath=${PWD}" -asmflags "all=-trimpath=${PWD}" -ldflags "-extldflags ${LDFLAGS}" -buildmode=pie

  cd "${srcdir}/${pkgname%-git}/cmd/qtrcc"
  go build -gcflags "all=-trimpath=${PWD}" -asmflags "all=-trimpath=${PWD}" -ldflags "-extldflags ${LDFLAGS}" -buildmode=pie

  cd "${srcdir}/${pkgname%-git}/cmd/qtsetup"
  go build -gcflags "all=-trimpath=${PWD}" -asmflags "all=-trimpath=${PWD}" -ldflags "-extldflags ${LDFLAGS}" -buildmode=pie
}

package() {
  cd "${pkgname%-git}/cmd"
  install -Dm755 qtdeploy/qtdeploy "$pkgdir"/usr/bin/qtdeploy
  install -Dm755 qtminimal/qtminimal "$pkgdir"/usr/bin/qtminimal
  install -Dm755 qtmoc/qtmoc "$pkgdir"/usr/bin/qtmoc
  install -Dm755 qtrcc/qtrcc "$pkgdir"/usr/bin/qtrcc
  install -Dm755 qtsetup/qtsetup "$pkgdir"/usr/bin/qtsetup
}
