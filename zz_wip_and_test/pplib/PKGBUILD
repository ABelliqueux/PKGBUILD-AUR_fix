# Maintainer: 

pkgname=pplib
pkgver=7.0.1
pkgrel=1
pkgdesc="Collection of C++ functions and classes"
arch=(x86_64)
url="https://www.pfp.de/ppl/"
license=(BSD)
#depends=(pcre libtiff)
makedepends=(nasm pcre sdl2 lame libshout sqlite3 libtiff imagemagick)
source=("$pkgname-$pkgver.tar.gz::https://github.com/pfedick/pplib/archive/REL_${pkgver//./_}.tar.gz"
        'https://raw.githubusercontent.com/FabioLolix/AUR-artifacts/master/pplib-MagickWand-header.patch')
sha256sums=('7cab7ca3c31be4726c4b45025bd49bda34239ade30853edd778d4851e808be37'
            'b73e73a3adde061db8425ce80e061df94d31461a3a71bfcd6d0db76a8f7116a6')

prepare() {
  cd "pplib-REL_${pkgver//./_}"
  patch -Np1 -i ../pplib-MagickWand-header.patch
}

build() {
  cd "pplib-REL_${pkgver//./_}"
  ./configure \
    --with-libtiff=/usr \
    --with-pcre=/usr \
    --with-jpeg \
    --with-nasm \
    --with-sqlite3 \
    --with-mysql=no \
    --with-postgresql=no \
    --prefix=/usr

  make
}

package() {
  cd "pplib-REL_${pkgver//./_}"
  make DESTDIR="$pkgdir" install
  install -D LICENSE.TXT -t "${pkgdir}/usr/share/licenses/${pkgname}"
}


