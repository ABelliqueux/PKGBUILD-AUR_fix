# Maintainer: Fabio 'Lolix' Loli <fabio.loli@disroot.org> -> https://github.com/FabioLolix
# Contributor: George Katevenis <george_kate[at]hotmail[dot]com>
# Contributor: Qirui Wang <wqr.prg@gmail.com>

pkgbase=wxwidgets-3.1
pkgname=(wxwidgets-gtk2-3.1 wxwidgets-gtk3-3.1 wxwidgets-gtk-common-3.1)
pkgver=3.1.5
pkgrel=1
arch=(x86_64)
url="https://wxwidgets.org"
license=('custom:wxWindows')
makedepends=(gst-plugins-base glu webkit2gtk libnotify gtk2)
options=(!emptydirs)
source=("https://github.com/wxWidgets/wxWidgets/releases/download/v${pkgver}/wxWidgets-${pkgver}.tar.bz2"
        wxwidgets-3.1.conf)
sha256sums=('d7b3666de33aa5c10ea41bb9405c40326e1aeb74ee725bb88f90f1d50270a224'
            '765b4e731ea9d853030efa345976a35f19d89b6614c484d4b44dcf21669a9fb4')

prepare() {
  cp -a wxWidgets-${pkgver} wxWidgets-${pkgver}-gtk3
}

build() {
  cd "wxWidgets-${pkgver}"
  ./configure --prefix=/opt/wxgtk-3.1 --libdir=/opt/wxgtk-3.1/lib --with-gtk=2 --with-opengl --enable-unicode \
    --enable-graphics_ctx --enable-mediactrl --with-regex=builtin \
    --with-libpng=sys --with-libxpm=sys --with-libjpeg=sys --with-libtiff=sys \
    --disable-precomp-headers
  make
  make -C locale allmo

  cd ../wxWidgets-${pkgver}-gtk3
  ./configure --prefix=/opt/wxgtk-3.1 --libdir=/opt/wxgtk-3.1/lib --with-gtk=3 --with-opengl --enable-unicode \
    --enable-graphics_ctx --enable-mediactrl --enable-webview --with-regex=builtin \
    --with-libpng=sys --with-libxpm=sys --with-libjpeg=sys --with-libtiff=sys \
    --disable-precomp-headers
  make
}

package_wxwidgets-gtk-common-3.1() {
  pkgdesc='Common libraries and headers for wxgtk2 and wxgtk3, v3.1.x installed in /opt/wxgtk-3.1/'
  depends=(zlib gcc-libs expat)

  cd "wxWidgets-${pkgver}"
  make DESTDIR="${pkgdir}" install
  rm -r "$pkgdir"/opt/wxgtk-3.1/{bin/wx-config,lib/{wx,libwx_gtk*}}

  install -D -m644  ../wxwidgets-3.1.conf -t "${pkgdir}/etc/ld.so.conf.d/"
  install -D -m644 docs/licence.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_wxwidgets-gtk2-3.1() {
  pkgdesc='GTK+2 implementation of wxWidgets API for GUI, v3.1.x installed in /opt/wxgtk-3.1/'
  depends=(gtk2 libgl gst-plugins-base-libs libsm libxxf86vm wxgtk-common-3.1 libnotify)

  cd "wxWidgets-${pkgver}"
  make DESTDIR="${pkgdir}" install
  rm -r "$pkgdir"/opt/wxgtk-3.1/{include,share,lib/libwx_base*,bin/wxrc*}

  install -D -m644 docs/licence.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_wxwidgets-gtk3-3.1() {
  pkgdesc='GTK+3 implementation of wxWidgets API for GUI, v3.1.x installed in /opt/wxgtk-3.1/'
  depends=(gtk3 gst-plugins-base-libs libsm libxxf86vm wxgtk-common-3.1 libnotify)
  optdepends=('webkit2gtk: for webview support')

  cd "wxWidgets-${pkgver}"
  make DESTDIR="${pkgdir}" install
  rm -r "$pkgdir"/opt/wxgtk-3.1/{include,share,lib/libwx_base*,bin/wxrc*}
  mv "$pkgdir"/opt/wxgtk-3.1/bin/wx-config{,-gtk3}

  install -D -m644 docs/licence.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
