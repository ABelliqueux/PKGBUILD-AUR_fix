# Maintainer: George Katevenis <george_kate[at]hotmail[dot]com>
# wxgtk-dev (non-opt) maintainer: Qirui Wang <wqr.prg@gmail.com>

pkgbase=wxgtk-3.0.5
pkgname=(wxgtk2-3.0.5 wxgtk3-3.0.5 wxgtk-common-3.0.5)
pkgver=3.0.5
pkgrel=1
arch=(x86_64)
url="https://wxwidgets.org"
license=('custom:wxWindows')
makedepends=(gst-plugins-base glu webkit2gtk libnotify gtk2)
options=(!emptydirs)
source=("https://github.com/wxWidgets/wxWidgets/releases/download/v${pkgver}/wxWidgets-${pkgver}.tar.bz2"
        wxgtk-3.0.5.conf)
sha256sums=('8aacd56b462f42fb6e33b4d8f5d40be5abc3d3b41348ea968aa515cc8285d813'
            'f89cd15e685a9614b1b5d55c202391a8eac81740c2bd1b51e53eb6362e9ffd80')

prepare() {
  cp -a wxWidgets-3.0.5 wxWidgets-3.0.5-gtk3
}

build() {
  cd wxWidgets-3.0.5
  ./configure --prefix=/opt/wxgtk-3.0.5 --libdir=/opt/wxgtk-3.0.5/lib --with-gtk=2 --with-opengl --enable-unicode \
    --enable-graphics_ctx --enable-mediactrl --with-regex=builtin \
    --with-libpng=sys --with-libxpm=sys --with-libjpeg=sys --with-libtiff=sys \
    --disable-precomp-headers
  make
  make -C locale allmo

  cd ../wxWidgets-3.0.5-gtk3
  ./configure --prefix=/opt/wxgtk-3.0.5 --libdir=/opt/wxgtk-3.0.5/lib --with-gtk=3 --with-opengl --enable-unicode \
    --enable-graphics_ctx --enable-mediactrl --enable-webview --with-regex=builtin \
    --with-libpng=sys --with-libxpm=sys --with-libjpeg=sys --with-libtiff=sys \
    --disable-precomp-headers
  make
}

package_wxgtk-common-3.0.5() {
  pkgdesc='Common libraries and headers for wxgtk2 and wxgtk3, v3.0.5 installed in /opt/wxgtk-3.0.5/'
  depends=(zlib gcc-libs expat)

  cd wxWidgets-3.0.5
  make DESTDIR="${pkgdir}" install
  rm -r "$pkgdir"/opt/wxgtk-3.0.5/{bin/wx-config,lib/{wx,libwx_gtk*}}

  install -D -m644  ../wxgtk-3.0.5.conf -t "${pkgdir}/etc/ld.so.conf.d/"
  install -D -m644 docs/licence.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_wxgtk2-3.0.5() {
  pkgdesc='GTK+2 implementation of wxWidgets API for GUI, v3.0.5 installed in /opt/wxgtk-3.0.5/'
  depends=(gtk2 libgl gst-plugins-base-libs libsm libxxf86vm wxgtk-common-3.0.5 libnotify)

  cd wxWidgets-3.0.5
  make DESTDIR="${pkgdir}" install
  rm -r "$pkgdir"/opt/wxgtk-3.0.5/{include,share,lib/libwx_base*,bin/wxrc*}

  install -D -m644 docs/licence.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_wxgtk3-3.0.5() {
  pkgdesc='GTK+3 implementation of wxWidgets API for GUI, v3.0.5 installed in /opt/wxgtk-3.0.5/'
  depends=(gtk3 gst-plugins-base-libs libsm libxxf86vm wxgtk-common-3.0.5 libnotify)
  optdepends=('webkit2gtk: for webview support')

  cd wxWidgets-3.0.5-gtk3
  make DESTDIR="${pkgdir}" install  
  rm -r "$pkgdir"/opt/wxgtk-3.0.5/{include,share,lib/libwx_base*,bin/wxrc*}
  mv "$pkgdir"/opt/wxgtk-3.0.5/bin/wx-config{,-gtk3}

  install -D -m644 docs/licence.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
