# Maintainer: Jun Bo Bi <jambonmcyeah@gmail.com>

pkgname=netcoredbg-git
pkgver=1.2.0.761.r0.ge23f439
pkgrel=1
pkgdesc="Debugger for .NET Core runtime"
url="https://github.com/Samsung/netcoredbg"
license=(MIT)
arch=(x86_64)
depends=(dotnet-host dotnet-runtime dotnet-sdk)
makedepends=(git cmake clang)
source=("git+https://github.com/Samsung/netcoredbg.git"
        "git+https://github.com/dotnet/coreclr.git")
sha256sums=('SKIP'
            'SKIP')

pkgver() {
  cd "${pkgname%-git}"
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd "${pkgname%-git}"
  install -d build
  cp -a "${srcdir}"/coreclr "${srcdir}"/netcoredbg/.coreclr
}

build() {
  cd "${pkgname%-git}/build"
  export CC=clang CXX=clang++

  cmake .. \
    -DCMAKE_INSTALL_PREFIX=/usr/bin \
    -DDOTNET_DIR=/usr/share/dotnet
  make

}

package() {
  cd "${pkgname%-git}/build"
  make DESTDIR="$pkgdir" install
  install -Dm644 ../LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
