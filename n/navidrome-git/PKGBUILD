# Maintainer: Sumner Evans <me [at] sumnerevans [dot] com>

pkgname=navidrome-git
pkgver=0.40.0.r1.gd8a4db36
pkgrel=1
pkgdesc="Music Server and Streamer compatible with Subsonic/Airsonic"
url='https://www.navidrome.org/'
license=(GPL3)
arch=(x86_64 armv6h armv7h aarch64)
provides=(navidrome)
conflicts=(navidrome)
makedepends=(go git npm nodejs-lts-fermium)
depends=(ffmpeg)
source=('git+https://github.com/deluan/navidrome.git')
sha256sums=('SKIP')

pkgver() {
  cd "${srcdir}/${pkgname%-git}"
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd "${srcdir}/${pkgname%-git}"
  export GOPATH="$SRCDEST/go-cache"
  make setup
  make buildall
}

package() {
  install -Dm755 "${srcdir}/${pkgname%-git}/navidrome" "${pkgdir}/usr/bin/navidrome"
}

