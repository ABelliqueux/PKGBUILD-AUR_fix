# Maintainer: Fabio 'Lolix' Loli <fabio.loli@disroot.org> -> https://github.com/FabioLolix
# Contributor: Helder Bertoldo <helder.bertoldo@gmail.com>

pkgname=torrential-git
pkgver=1.1.0.r16.g8c6d1ab
pkgrel=2
pkgdesc="Download torrents in style with this speedy, minimalist torrent client designed for Pantheon Shell"
arch=(i686 x86_64)
url="https://github.com/davidmhewitt/torrential"
license=(GPL2)
depends=(libgranite.so libevent libnatpmp miniupnpc)
makedepends=(git cmake vala)
provides=(torrential)
conflicts=(torrential)
source=("git+https://github.com/davidmhewitt/torrential.git"
        "torrential-transmission::git+https://github.com/davidmhewitt/transmission.git"
        "git+https://github.com/transmission/dht.git"
        "git+https://github.com/transmission/libb64.git"
        "git+https://github.com/transmission/libevent.git"
        "git+https://github.com/transmission/libnatpmp.git"
        "git+https://github.com/transmission/libutp.git"
        "git+https://github.com/transmission/miniupnpc.git"
        "torrential-no-unity.patch::https://git.archlinux.org/svntogit/community.git/plain/trunk/no-unity.patch")
sha256sums=('SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            '8b279ca266c8b1643cf10ff0a66a66283be6595959720bcc321fdef0f3da6ea7')

pkgver() {
  cd "${pkgname%-git}"
  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd "${pkgname%-git}"
  install -d build

  git submodule init
  git config 'submodule.transmission.url' "${srcdir}/torrential-transmission"
  git submodule update

  cd transmission
  git submodule init
  git submodule set-url third-party/dht "$srcdir/dht"
  git submodule set-url third-party/libb64 "$srcdir/libb64"
  git submodule set-url third-party/libevent "$srcdir/libevent"
  git submodule set-url third-party/libnatpmp "$srcdir/libnatpmp"
  git submodule set-url third-party/libutp "$srcdir/libutp"
  git submodule set-url third-party/miniupnpc "$srcdir/miniupnpc"
  git submodule update

  cd ..
  patch -Np1 -i ../torrential-no-unity.patch
  sed -i '/--fatal-warnings/d' CMakeLists.txt
}

build() {
  cd "${pkgname%-git}/build"
  cmake .. \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${pkgname%-git}/build"
  make DESTDIR="${pkgdir}" install
  ln -s /usr/bin/com.github.davidmhewitt.torrential "$pkgdir/usr/bin/torrential"
}

